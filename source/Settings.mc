import Toybox.Application;
import Toybox.Lang;
import Toybox.Graphics;
import Toybox.System;
import Toybox.Application;
import Toybox.Communications;
import Toybox.WatchUi;
import Toybox.PersistedContent;

enum /* TrackStyle */ {
    TRACK_STYLE_LINE = 0, // Standard continuous line
    TRACK_STYLE_DASHED = 1, // Interpolated dashes
    TRACK_STYLE_POINTS = 2, // Dots only at actual data points
    TRACK_STYLE_POINTS_INTERPOLATED = 3, // Dots spaced evenly along the path
    TRACK_STYLE_BOXES = 4, // Squares outline only at actual data points
    TRACK_STYLE_BOXES_INTERPOLATED = 5, // Squares outline spaced evenly along the path
    TRACK_STYLE_FILLED_SQUARE = 6, // Squares only at actual data points
    TRACK_STYLE_FILLED_SQUARE_INTERPOLATED = 7, // Squares spaced evenly along the path
    TRACK_STYLE_POINTS_OUTLINE = 8, // Dots only at actual data points, just the circle outline
    TRACK_STYLE_POINTS_OUTLINE_INTERPOLATED = 9, // Dots spaced evenly along the path, just the circle outline
    TRACK_STYLE_CHECKERBOARD = 10,
    TRACK_STYLE_HAZARD = 11,
    TRACK_STYLE_DOT_MATRIX = 12,
    TRACK_STYLE_POLKA_DOT = 13,
    TRACK_STYLE_DIAMOND = 14,

    TRACK_STYLE_MAX,
}

enum /*TrackPointReductionMethod*/ {
    TRACK_POINT_REDUCTION_METHOD_DOWNSAMPLE = 0,
    TRACK_POINT_REDUCTION_METHOD_REUMANN_WITKAM = 1,

    TRACK_POINT_REDUCTION_METHOD_MAX,
}

enum /*DataType*/ {
    DATA_TYPE_NONE = 0,
    DATA_TYPE_SCALE = 1,
    DATA_TYPE_ALTITUDE = 2,
    DATA_TYPE_AVERAGE_HEART_RATE = 3,
    DATA_TYPE_AVERAGE_SPEED = 4,
    DATA_TYPE_CURRENT_HEART_RATE = 5,
    DATA_TYPE_CURRENT_SPEED = 6,
    DATA_TYPE_ELAPSED_DISTANCE = 7,
    DATA_TYPE_ELAPSED_TIME = 8,
    DATA_TYPE_TOTAL_ASCENT = 9,
    DATA_TYPE_TOTAL_DESCENT = 10,
    DATA_TYPE_AVERAGE_PACE = 11,
    DATA_TYPE_CURRENT_PACE = 12,
    DATA_TYPE_WALL_CLOCK = 13,
    DATA_TYPE_CURRENT_LAP_TIME = 14,
    DATA_TYPE_CURRENT_LAP_PACE = 15,
    DATA_TYPE_LAST_LAP_TIME = 16,
    DATA_TYPE_LAST_LAP_PACE = 17,
    DATA_TYPE_GRADE = 18,
    DATA_TYPE_HEADING = 19,
    DATA_TYPE_GPS_ACCURACY = 20,
    DATA_TYPE_CURRENT_LAP_DISTANCE = 21,

    // other metrics that might be good
    // most of these are inbuilt garmin ones (so could easily be added to a second data screen)
    // Ill add them if users ask, but currently only have requests for pace https://github.com/pauljohnston2025/breadcrumb-garmin/issues/8
    DATA_TYPE_MAX,
}

enum /*Mode*/ {
    MODE_NORMAL = 0,
    MODE_ELEVATION = 1,
    MODE_MAP_MOVE = 2,
    MODE_DEBUG = 3,
    MODE_MAP_MOVE_ZOOM = 4, // mostly for app (and button presses), but also allows larger touch zones
    MODE_MAP_MOVE_UP_DOWN = 5, // mostly for app (and button presses), but also allows larger touch zones
    MODE_MAP_MOVE_LEFT_RIGHT = 6, // mostly for app (and button presses), but also allows larger touch zones

    MODE_MAX,
}

enum /*ElevationMode*/ {
    ELEVATION_MODE_STACKED,
    ELEVATION_MODE_ORDERED_ROUTES,
    ELEVATION_MODE_MAX,
}

enum /*ZoomMode*/ {
    ZOOM_AT_PACE_MODE_PACE,
    ZOOM_AT_PACE_MODE_STOPPED,
    ZOOM_AT_PACE_MODE_NEVER_ZOOM,
    ZOOM_AT_PACE_MODE_ALWAYS_ZOOM,
    ZOOM_AT_PACE_MODE_SHOW_ROUTES_WITHOUT_TRACK,
    ZOOM_AT_PACE_MODE_MAX,
}

enum /*UiMode*/ {
    UI_MODE_SHOW_ALL, // show a heap of ui elements on screen always
    UI_MODE_HIDDEN, // ui still active, but is hidden
    UI_MODE_NONE, // no touch ui (tap events disabled)
    UI_MODE_SHOW_TOUCH_ONLY, // only show touch ui (buttons still work, they are just hidden)
    UI_MODE_SHOW_BUTTONS_ONLY, // only button ui (touch still works, its just hidden)
    UI_MODE_MAX,
}

enum /*RenderMode*/ {
    RENDER_MODE_BUFFERED_ROTATING,
    RENDER_MODE_UNBUFFERED_ROTATING,
    RENDER_MODE_BUFFERED_NO_ROTATION,
    RENDER_MODE_UNBUFFERED_NO_ROTATION,
    RENDER_MODE_MAX,
}

enum /*RenderMode*/ {
    ALERT_TYPE_TOAST = 0,
    // ALERT_TYPE_ALERT, // not used in app mode
    ALERT_TYPE_IMAGE = 2,
    ALERT_TYPE_MAX,
}

enum /*AttributionType*/ {
    /*ATTRIBUTION_GOOGLE,*/
    ATTRIBUTION_OPENTOPOMAP = 1,
    ATTRIBUTION_ESRI,
    ATTRIBUTION_OPENSTREETMAP,
    ATTRIBUTION_STADIA,
    ATTRIBUTION_CARTO,
    ATTRIBUTION_MAPY,
}

enum /*UrlPrefix*/ {
    URL_PREFIX_NONE,
    URL_PREFIX_ESRI,
    URL_PREFIX_STADIA,
    URL_PREFIX_CARTO,
    URL_PREFIX_MAPY,
}

enum /*AuthTokenType*/ {
    AUTH_TOKEN_TYPE_NONE,
    AUTH_TOKEN_TYPE_STADIA,
    AUTH_TOKEN_TYPE_MAPY,
}

const COMPANION_APP_TILE_URL = "http://127.0.0.1:8080";
const COMPANION_APP_TILE_URL_MATCH = "127.0.0.1"; // any localhost url should be the companion app, but maybe they changed the port on the companion app

(:imageTiles)
class TileServerInfo {
    var attributionType as Number;
    var urlPrefix as Number;
    var authTokenType as Number;
    var urlTemplate as String;
    var tileLayerMin as Number;
    var tileLayerMax as Number;
    function initialize(
        attributionType as Number,
        urlPrefix as Number,
        authTokenType as Number,
        urlTemplate as ResourceId,
        tileLayerMin as Number,
        tileLayerMax as Number
    ) {
        me.attributionType = attributionType;
        me.urlPrefix = urlPrefix;
        me.authTokenType = authTokenType;
        me.urlTemplate = WatchUi.loadResource(urlTemplate) as String;
        me.tileLayerMin = tileLayerMin;
        me.tileLayerMax = tileLayerMax;
    }
}

(:imageTiles)
function getUrlPrefix(prefix as Number) as String {
    if (prefix == URL_PREFIX_NONE) {
        return "";
    } else if (prefix == URL_PREFIX_ESRI) {
        return WatchUi.loadResource(Rez.Strings.esriPrefix) as String;
    } else if (prefix == URL_PREFIX_STADIA) {
        return WatchUi.loadResource(Rez.Strings.stadiaPrefix) as String;
    } else if (prefix == URL_PREFIX_CARTO) {
        return WatchUi.loadResource(Rez.Strings.cartoPrefix) as String;
    } else if (prefix == URL_PREFIX_MAPY) {
        return WatchUi.loadResource(Rez.Strings.mapyPrefix) as String;
    }

    return "";
}

(:imageTiles)
function getAuthTokenSuffix(type as Number) as String {
    switch (type) {
        case AUTH_TOKEN_TYPE_NONE:
            return "";
        case AUTH_TOKEN_TYPE_STADIA:
            return "?api_key={authToken}";
        case AUTH_TOKEN_TYPE_MAPY:
            return "?apiKey={authToken}";
    }

    return "";
}

(:noImageTiles)
function getTileServerInfo(id as Number) as TileServerInfo? {
    return null;
}

(:highMemory)
function getTileServerInfo(id as Number) as TileServerInfo? {
    // prettier-ignore
    // This is an array instead of a dict because dict does not render correctly, also arrays are faster
    // 0 => null, // special custom (no tile property changes will happen)
    // 1 => null, // special companion app (only the tileUrl will be updated)
    switch(id)
    {
        case 2:
            // open topo
            return new TileServerInfo(ATTRIBUTION_OPENTOPOMAP, URL_PREFIX_NONE, AUTH_TOKEN_TYPE_NONE, Rez.Strings.openTopoMapUrlTemplate, 0, 15); // OpenTopoMap
            // google - cannot use returns 404 - works from companion app (userAgent sent)
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=y&x={x}&y={y}&z={z}", 0, 20), // "Google - Hybrid"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=s&x={x}&y={y}&z={z}", 0, 20), // "Google - Satellite"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=m&x={x}&y={y}&z={z}", 0, 20), // "Google - Road"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=p&x={x}&y={y}&z={z}", 0, 20), // "Google - Terain"
            // arcgis (esri) - note some of these have been removed due to not enough coverage, and others have had layermin/max altered for australian coverage
            // _Reference maps are all the same - just the location names removing them
            // Note: when testing on the simulator, some of theese occasionaly seem to produce   
            // Error: Invalid Value
            // Details: failed inside handle_image_callback
        case 3:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldImageryTemplate, 0, 20); // Esri - World Imagery
        case 4:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldStreetMapTemplate, 0, 19); // Esri - World Street Map
        case 5:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldTopoMapTemplate, 0, 19); // Esri - World Topo Map
        case 6:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldTransportationTemplate, 0, 15); // Esri - World Transportation
        case 7:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldDarkGrayBaseTemplate, 0, 16); // Esri - World Dark Gray Base
        case 8:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldHillshadeTemplate, 0, 16); // Esri - World Hillshade
        case 9:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldHillshadeDarkTemplate, 0, 16); // Esri - World Hillshade Dark
        case 10:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldLightGrayBaseTemplate, 0, 16); // Esri - World Light Gray Base
        case 11:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriUsaTopoMapsTemplate, 0, 15); // Esri - USA Topo Maps
        case 12:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldOceanBaseTemplate, 0, 13); // Esri - World Ocean Base
        case 13:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldShadedReliefTemplate, 0, 13); // Esri - World Shaded Relief
        case 14:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriNatgeoWorldMapTemplate, 0, 12); // Esri - NatGeo World Map
        case 15:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldNavigationChartsTemplate, 0, 10); // Esri - World Navigation Charts
        case 16:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldPhysicalMapTemplate, 0, 8); // Esri - World Physical Map
        case 17:
            // https://wiki.openstreetmap.org/wiki/Raster_tile_providers
            return new TileServerInfo(ATTRIBUTION_OPENSTREETMAP, URL_PREFIX_NONE, AUTH_TOKEN_TYPE_NONE, Rez.Strings.openstreetmapCyclosmTemplate, 0, 20); // OpenStreetMap - cyclosm
        case 18:
            // stadia (also includes stamen) https://docs.stadiamaps.com/themes/
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaAlidadeSmoothTemplate, 0, 20); // Stadia - Alidade Smooth (auth required)
        case 19:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaAlidadeSmoothDarkTemplate, 0, 20); // Stadia - Alidade Smooth Dark (auth required)
        case 20:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaOutdoorsTemplate, 0, 20); // Stadia - Outdoors (auth required)
        case 21:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaStamenTonerTemplate, 0, 20); // Stadia - Stamen Toner (auth required)
        case 22:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaStamenTonerLiteTemplate, 0, 20); // Stadia - Stamen Toner Lite (auth required)
        case 23:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaStamenTerrainTemplate, 0, 20); // Stadia - Stamen Terrain (auth required)
        case 24:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaStamenWatercolorTemplate, 0, 16); // Stadia - Stamen Watercolor (auth required)
        case 25:
            return new TileServerInfo(ATTRIBUTION_STADIA, URL_PREFIX_STADIA, AUTH_TOKEN_TYPE_STADIA, Rez.Strings.stadiaOsmBrightTemplate, 0, 20); // Stadia - OSM Bright (auth required)
        case 26:
            // carto
            return new TileServerInfo(ATTRIBUTION_CARTO, URL_PREFIX_CARTO, AUTH_TOKEN_TYPE_NONE, Rez.Strings.cartoVoyagerTemplate, 0, 20); // Carto - Voyager
        case 27:
            return new TileServerInfo(ATTRIBUTION_CARTO, URL_PREFIX_CARTO, AUTH_TOKEN_TYPE_NONE, Rez.Strings.cartoDarkMatterTemplate, 0, 20); // Carto - Dark Matter
        case 28:
            return new TileServerInfo(ATTRIBUTION_CARTO, URL_PREFIX_CARTO, AUTH_TOKEN_TYPE_NONE, Rez.Strings.cartoLightAllTemplate, 0, 20); // Carto - Light All
        case 29:
            // Mapy
            return new TileServerInfo(ATTRIBUTION_MAPY, URL_PREFIX_MAPY, AUTH_TOKEN_TYPE_MAPY, Rez.Strings.mapyBasicTemplate, 0, 19); // Mapy - Basic
        case 30:
            return new TileServerInfo(ATTRIBUTION_MAPY, URL_PREFIX_MAPY, AUTH_TOKEN_TYPE_MAPY, Rez.Strings.mapyOutdoorTemplate, 0, 19); // Mapy - Outdoor
        case 31:
            return new TileServerInfo(ATTRIBUTION_MAPY, URL_PREFIX_MAPY, AUTH_TOKEN_TYPE_MAPY, Rez.Strings.mapyWinterTemplate, 0, 19); // Mapy - Winter
        case 32:
            return new TileServerInfo(ATTRIBUTION_MAPY, URL_PREFIX_MAPY, AUTH_TOKEN_TYPE_MAPY, Rez.Strings.mapyAerialTemplate, 0, 13); // Mapy - Aerial
    }
    return null;
}

(:lowMemory)
function getTileServerInfo(id as Number) as TileServerInfo? {
    // prettier-ignore
    // This is an array instead of a dict because dict does not render correctly, also arrays are faster
    // 0 => null, // special custom (no tile property changes will happen)
    // 1 => null, // special companion app (only the tileUrl will be updated)
    switch(id)
    {
        case 2:
            // open topo
            return new TileServerInfo(ATTRIBUTION_OPENTOPOMAP, URL_PREFIX_NONE, AUTH_TOKEN_TYPE_NONE, Rez.Strings.openTopoMapUrlTemplate, 0, 15); // OpenTopoMap
            // google - cannot use returns 404 - works from companion app (userAgent sent)
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=y&x={x}&y={y}&z={z}", 0, 20), // "Google - Hybrid"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=s&x={x}&y={y}&z={z}", 0, 20), // "Google - Satellite"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=m&x={x}&y={y}&z={z}", 0, 20), // "Google - Road"
            // new TileServerInfo(ATTRIBUTION_GOOGLE, "https://mt1.google.com/vt/lyrs=p&x={x}&y={y}&z={z}", 0, 20), // "Google - Terain"
            // arcgis (esri) - note some of these have been removed due to not enough coverage, and others have had layermin/max altered for australian coverage
            // _Reference maps are all the same - just the location names removing them
            // Note: when testing on the simulator, some of theese occasionaly seem to produce   
            // Error: Invalid Value
            // Details: failed inside handle_image_callback
        case 3:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldImageryTemplate, 0, 20); // Esri - World Imagery
        case 4:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldStreetMapTemplate, 0, 19); // Esri - World Street Map
        case 5:
            return new TileServerInfo(ATTRIBUTION_ESRI, URL_PREFIX_ESRI, AUTH_TOKEN_TYPE_NONE, Rez.Strings.esriWorldTopoMapTemplate, 0, 19); // Esri - World Topo Map
    }
    return null;
}

(:companionTiles)
class TileUpdateHandler {
    var mapChoiceVersion as Number;
    function initialize(_mapChoiceVersion as Number) {
        mapChoiceVersion = _mapChoiceVersion;
    }

    function handle(
        responseCode as Number,
        data as
            Dictionary or
                String or
                Iterator or
                WatchUi.BitmapResource or
                Graphics.BitmapReference or
                Null
    ) as Void {
        if (responseCode != 200) {
            logE("failed TUH: " + responseCode);
            openTileServer();
            return;
        }

        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        var settings = _breadcrumbContextLocal.settings;
        if (settings.mapChoiceVersion != mapChoiceVersion || settings.mapChoice != 1) {
            // we have either changed version (and sent out another request that will update), or its no longer the companion app set as the desired choice
            return;
        }

        if (!(data instanceof Dictionary)) {
            logE("failed TUH: wrong type" + data);
            return;
        }

        settings.companionChangedToMaxMin(
            data["tileLayerMin"] as Number,
            data["tileLayerMax"] as Number
        );
    }

    function openTileServer() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        // PROTOCOL_SEND_OPEN_APP will not work if the tile server is disabled :(
        // also send a toast
        _breadcrumbContextLocal.webRequestHandler.transmit(
            [PROTOCOL_SEND_OPEN_APP],
            {},
            new CommStatus()
        );
        WatchUi.showToast("Tile Server Response Failed", {});
    }
}

const DATA_PAGE_BASE_ID = 100;

// we are getting dangerously close to the app settings limit
// was getting "Unable to serialize app data" in the sim, but after a restart worked fine
// see
// https://forums.garmin.com/developer/connect-iq/f/discussion/409127/unable-to-serialize-app-data---watch-app?pifragment-1298=1#pifragment-1298=1
// is seems like this only happened when:
// * I tried to run on instinct 3 (that only has 128kb of memory)
// * Crashed with OOM
// * Then tried running on the venu2s which has enough memory it fails with "Unable to serialize app data"
// * Reset sim app data and remove apps
// * Works fine
class Settings {
    var sport as Number = Activity.SPORT_GENERIC;
    var subSport as Number = Activity.SUB_SPORT_GENERIC;

    // todo only load these when needed (but cache them)
    (:imageTiles)
    var attributionImage as WatchUi.BitmapResource =
        WatchUi.loadResource(Rez.Drawables.OpenTopMapAttribution) as BitmapResource;
    var attributionImageType as Number = ATTRIBUTION_OPENTOPOMAP;

    // switches the stop button to be the same as start (conventional garmin layout)
    // defaults to false to keep our layout consistent with the other breadcrumb datafields (top right is the mode, so start button toggles mode)
    var useStartForStop as Boolean = false;

    // should be a multiple of 256 (since thats how tiles are stored, though the companion app will render them scaled for you)
    // we will support rounding up though. ie. if we use 50 the 256 tile will be sliced into 6 chunks on the phone, this allows us to support more pixel sizes.
    // so math.ceil should be used what figuring out how many meters a tile is.
    // eg. maybe we cannot do 128 but we can do 120 (this would limit the number of tiles, but the resolution would be slightly off)

    // The smaller tile size, mainly for use with companion app, allows slicing scaledTileSize into smaller tiles
    (:companionTiles)
    var tileSize as Number = 64;
    (:noCompanionTiles)
    var tileSize as Number = 192;

    var fullTileSize as Number = 256; // The tile size on the tile server
    // The tile size to scale images to, results in significantly smaller downloads (and faster speeds) but makes image slightly blurry.
    // 190 seems to be a good compromise between speed and crisp images. it does not effect the image too much, but gives us about 2X the speed.
    // 128 is a bit too blurry, but would be fine on some maps (anything without words)
    var scaledTileSize as Number = 192; // should be a multiple of the default tileSize
    // website says: Worldwide, Zoom to 17. (Zoom limited to 15 on website opentopomap.org)
    // real world test showed 17 produced errors (maybe you need to be authed to get this?)
    var tileLayerMax as Number = 15;
    var tileLayerMin as Number = 0;
    // there is both a memory limit to the number of tiles we can store, as well as a storage limit
    // for now this is both, though we may be able to store more than we can in memory
    // so we could use the storage as a tile cache, and revert to loading from there, as it would be much faster than
    // fetching over bluetooth
    // not sure if we can even store bitmaps into storage, it says only BitmapResource
    // id have to serialise it to an array and back out (might not be too hard)
    // 64 is enough to render outside the screen a bit 64*64 tiles with 64 tiles gives us 512*512 worth of pixel data

    // did some tests in the sim (render mode buffered rotations - so we also have the giant scratchpad bitmap active, suspect that why memory starts at ~115.0k)
    // no tiles 114.8kb used memory
    // 36 64*64 tiles 132.5kb = ~0.491
    // 64 64*64 tiles 144.9kb = ~0.4703125 per 64*64 tile
    // make image request
    // cleared 115.0k
    // 18 192*192 tiles 120.3k = ~0.294k per tile - less than the 64*64 tiles possibly because it stored as an optimised png image instead of a bitmap?
    // 64 64*64 tiles 145.0k = ~0.468 per tile
    // what I did notice though is that I can have many more tiles of 64*64 even though each tile is larger. The 192*192 image tiles crash the system at ~20 tiles with OOM errors.
    // Think its not registering correctly with System.getSystemStats().usedMemory, since its graphics memory pool
    // 95 64*64 tiles 180.0k (though sim was spitting out error saying it could not render) = ~ 0.684
    // so its ~0.000146484375k per pixel
    // there appears to be some overhead though

    // with render mode unbuffered roatations (no scratchpad bitmap)
    // 100 64*64 tiles 174.0k
    // cleared after we are now at 132.4K - go figure, larger than with the scratchpad

    // restart sim with nothing render mode unbuffered rotations (no scratchpad bitmap)
    // cleared - 114.9K
    // changed render mode to buffered rotations - 115.1K so scratchpad has almost 0 effect?
    // a small route is like 3K
    // graphics memory pool is

    // using the memory view (which crashes constantly) instead of the on device System.getSystemStats().usedMemory
    // graphics pool memeory only
    // 36 64*64 tiles 16496b =  .45822k per tile          // note: this is about the same as previous calcs, image resources must be stored differently to bufferedbitmaps
    // 13 192X192 tiles 73840b = 5.680k per tile  0.000154k per pixel - consistent with previous calcs

    const BYTES_PER_PIXEL = 0.15f;
    (:companionTiles)
    var tileCacheSize as Number = 64;
    (:noCompanionTiles)
    var tileCacheSize as Number = 8;
    var mode as Number = MODE_NORMAL;
    var modeDisplayOrder as Array<Number> = [0, 1, 2];
    var elevationMode as Number = ELEVATION_MODE_STACKED;
    var mapEnabled as Boolean = false;
    // cache the tiles in storage when they are loaded, allows for fully offline maps
    // unfortunately buffered bitmaps cannot be stored into storage (resources and BitMapResources can be, but not the buffered kind)
    // so we need to store the result of makeImageRequest or makeWebRequest
    var cacheTilesInStorage as Boolean = false;
    var storageMapTilesOnly as Boolean = false;
    // storage seems to fill up around 200 with 192*192 tiles from image requests
    // can be much larger for companion app is used, since the tiles can be much smaller with TILE_DATA_TYPE_BASE64_FULL_COLOUR
    // saw a crash around 513 tiles, which would be from our internal array StorageTileCache._tilesInStorage
    var storageTileCacheSize as Number = 150;
    var storageSeedBoundingBox as Boolean = false; // seed entire bounding box
    var storageSeedRouteDistanceM as Float = 10f; // if seeding based on route (storageSeedBoundingBox = false) seed this far around the route
    var storageTileCachePageCount as Number = 1;

    (:blackAndWhite)
    var trackColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    var trackColour2 as Number = Graphics.COLOR_BLACK;
    (:blackAndWhite)
    var defaultRouteColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    var elevationColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    var userColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    const DEFAULT_ROUTE_WIDTH = 2;

    (:fullColours)
    var trackColour as Number = Graphics.COLOR_GREEN;
    (:fullColours)
    var trackColour2 as Number = Graphics.COLOR_TRANSPARENT;
    const DEFAULT_ROUTE_COLOUR_2 = Graphics.COLOR_TRANSPARENT;
    const DEFAULT_ROUTE_STYLE = TRACK_STYLE_LINE;
    (:fullColours)
    const DEFAULT_ROUTE_WIDTH = 4;
    (:fullColours)
    var defaultRouteColour as Number = Graphics.COLOR_BLUE;
    (:fullColours)
    var elevationColour as Number = Graphics.COLOR_ORANGE;
    (:fullColours)
    var userColour as Number = Graphics.COLOR_ORANGE;
    // this should probably be the same as tileCacheSize? since there is no point having 20 outstanding if we can only store 10 of them
    var maxPendingWebRequests as Number = 5;
    // Renders around the users position
    var metersAroundUser as Number = 500; // keep this fairly high by default, too small and the map tiles start to go blurry
    var centerUserOffsetY as Float = 0.5f; // fraction of the screen to move the user down the page 0.5 - user appears in center, 0.75 - user appears 3/4 down the screen. Useful to see more of the route in front of the user.
    var mapMoveScreenSize as Float = 0.3f; // how far to move the map when the user presses on screen buttons, a fraction of the screen size.
    var zoomAtPaceMode as Number = ZOOM_AT_PACE_MODE_PACE;
    var zoomAtPaceSpeedMPS as Float = 1.0; // meters per second
    var useTrackAsHeadingSpeedMPS as Float = 1000f; // meters per second
    var topDataType as Number = DATA_TYPE_NONE;
    var bottomDataType as Number = DATA_TYPE_SCALE;
    var dataFieldTextSize as Number = Graphics.FONT_XTINY;
    // dataFieldPageCounts is the number of pages on each screen
    // eg. 1,2,1 is 3 pages 1 datafield on page 1, 2 datafields on page 2, etc.
    // pages can have up to 4 dataqfields on them, the page count is not limited
    var dataFieldPageCounts as Array<Number> = [];
    // dataFieldPageTypes is the types of datafields on the pages eg.
    // 0,1,1,0 to show DATA_TYPE_NONE, DATA_TYPE_SCALE, DATA_TYPE_ALTITUDE and DATA_TYPE_NONE the number of  dataFieldPageTypes needs to match the number of dataFieldPageCounts added together
    var dataFieldPageTypes as Array<Number> = [];
    var autoLapDistanceM as Number = -1;
    var minTrackPointDistanceM as Number = 5; // minimum distance between 2 track points
    var trackPointReductionMethod as Number = TRACK_POINT_REDUCTION_METHOD_DOWNSAMPLE;
    var uiMode as Number = UI_MODE_SHOW_ALL;
    var fixedLatitude as Float? = null;
    var fixedLongitude as Float? = null;
    // supports place holders such as
    // use http://127.0.0.1:8080 for companion app
    // but you can also use something like https://a.tile.opentopomap.org/{z}/{x}/{y}.png
    // to make this work on the emulator you ned to run
    // adb forward tcp:8080 tcp:8080
    (:companionTiles)
    var tileUrl as String = COMPANION_APP_TILE_URL;
    (:noCompanionTiles)
    var tileUrl as String = "https://a.tile.opentopomap.org/{z}/{x}/{y}.png";
    var authToken as String = "";
    var requiresAuth as Boolean = false;
    (:companionTiles)
    var mapChoice as Number = 0;
    (:noCompanionTiles)
    var mapChoice as Number = 2;
    var mapChoiceVersion as Number = 0;
    // see keys below in routes = getArraySchema(...)
    // see oddity with route name and route loading new in context.newRoute
    var routes as Array<Dictionary> = [];
    var routesEnabled as Boolean = true;
    var disableMapsFailureCount as Number = 200; // 0 for unlimited
    var displayRouteNames as Boolean = true;
    (:blackAndWhite)
    var normalModeColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    var uiColour as Number = Graphics.COLOR_WHITE;
    (:blackAndWhite)
    var debugColour as Number = Graphics.COLOR_WHITE;

    (:fullColours)
    var normalModeColour as Number = Graphics.COLOR_BLUE;
    (:fullColours)
    var uiColour as Number = Graphics.COLOR_DK_GRAY;
    (:fullColours)
    var debugColour as Number = Graphics.COLOR_WHITE;
    // I did get up to 4 large routes working with off track alerts, but any more than that and watchdog catches us out, 3 is a safer limit.
    // currently we still load disabled routes into memory, so its also not great having this large and a heap of disabled routes
    private var _routeMax as Number = 3;

    // note this only works if a single track is enabled (multiple tracks would always error)
    var enableOffTrackAlerts as Boolean = true;
    var offTrackAlertsDistanceM as Number = 20;
    var offTrackAlertsMaxReportIntervalS as Number = 60;
    var offTrackCheckIntervalS as Number = 15;
    var alertType as Number = ALERT_TYPE_TOAST;
    var offTrackWrongDirection as Boolean = false;
    var drawCheverons as Boolean = false;

    var drawLineToClosestPoint as Boolean = true;
    var displayLatLong as Boolean = true;
    private var _scaleRestrictedToTileLayers as Boolean = false; // scale will be restricted to the tile layers - could do more optimised render in future
    // https://developer.garmin.com/connect-iq/api-docs/Toybox/Communications.html#PackingFormat-module
    // some devices do not support other colour pallets, and need to use default pallet and drawBitmap instead of drawBitmap2
    var packingFormat as Number = 1;
    var useDrawBitmap as Boolean = false; // implies scaleRestrictedToTileLayers, since the renders will be off if we do not

    // scratchpad used for rotations, but it also means we have a large bitmap stored around
    // I will also use that bitmap for re-renders though, and just do rotations every render rather than re-drawing all the tracks/tiles again
    var renderMode as Number = RENDER_MODE_BUFFERED_ROTATING;
    // how many seconds should we wait before even considering the next point
    // changes in speed/angle/zoom are not effected by this number. Though maybe they should be?
    var recalculateIntervalS as Number = 5;
    // pre seed tiles on either side of the viewable area
    var tileCachePadding as Number = 0;
    var httpErrorTileTTLS as Number = 60;
    var errorTileTTLS as Number = 20; // other errors are from garmin ble connection issues, retry faster by default
    var turnAlertTimeS as Number = -1; // -1 disables the check
    var minTurnAlertDistanceM as Number = -1; // -1 disables the check
    var maxTrackPoints as Number = 400;
    var trackStyle as Number = TRACK_STYLE_LINE;
    var trackWidth as Number = 4;

    // bunch of debug settings
    var drawLineToClosestTrack as Boolean = false;
    var showTileBorders as Boolean = false;
    var showErrorTileMessages as Boolean = false;
    var tileErrorColour as Number = Graphics.COLOR_BLACK;
    var drawHitBoxes as Boolean = false;
    var showDirectionPoints as Boolean = false;
    var showDirectionPointTextUnderIndex as Number = 0;

    // these settings can only be modified externally, but we cache them for faster/easier lookup
    // https://www.youtube.com/watch?v=LasrD6SZkZk&ab_channel=JaylaB
    var distanceImperialUnits as Boolean =
        System.getDeviceSettings().distanceUnits == System.UNIT_STATUTE;
    var elevationImperialUnits as Boolean =
        System.getDeviceSettings().elevationUnits == System.UNIT_STATUTE;
    var paceImperialUnits as Boolean =
        System.getDeviceSettings().paceUnits == System.UNIT_STATUTE;
    var is24Hour as Boolean = System.getDeviceSettings().is24Hour as Boolean;
    var trackTexture as Graphics.BitmapTexture or Number = -1; // -1 is to say use colour instead
    var routeTextures as Array<Graphics.BitmapTexture or Number> = []; // -1 is to say use colour instead

    (:lowMemory)
    function routeMax() as Number {
        return 1; // can only get 1 route (second route crashed on storage save), we also still need space for the track
    }

    (:highMemory)
    function routeMax() as Number {
        return _routeMax;
    }

    function setMode(_mode as Number) as Void {
        mode = _mode;
        // directly set mode, its only for what is displayed, which takes effect ont he next onUpdate
        // we do not want to call view.onSettingsChanged because it clears timestamps when going to the debug page.
        // The setValue method on this class calls the view changed method, so do not call it.
        Application.Properties.setValue("mode", mode);
    }

    (:settingsView)
    function setElevationMode(value as Number) as Void {
        elevationMode = value;
        setValue("elevationMode", elevationMode);
    }

    (:settingsView)
    function setUiMode(_uiMode as Number) as Void {
        uiMode = _uiMode;
        setValue("uiMode", uiMode);
    }

    (:settingsView)
    function setAlertType(_alertType as Number) as Void {
        alertType = _alertType;
        setValue("alertType", alertType);
    }

    (:settingsView)
    function setRenderMode(_renderMode as Number) as Void {
        renderMode = _renderMode;
        setValue("renderMode", renderMode);
        updateCachedValues();
        updateViewSettings();
    }

    function setFixedPositionRaw(lat as Float, long as Float) as Void {
        // hack method so that cached values can update the settings without reloading itself
        // its guaranteed to only be when moving around, and will never go to null
        fixedLatitude = lat;
        fixedLongitude = long;
        Application.Properties.setValue("fixedLatitude", lat);
        Application.Properties.setValue("fixedLongitude", long);
        clearPendingWebRequests();
    }

    function setFixedPosition(lat as Float?, long as Float?, clearRequests as Boolean) as Void {
        // logT("moving to: " + lat + " " + long);
        // be very careful about putting null into properties, it breaks everything
        if (lat == null || !(lat instanceof Float)) {
            lat = 0f;
        }
        if (long == null || !(long instanceof Float)) {
            long = 0f;
        }
        fixedLatitude = lat;
        fixedLongitude = long;
        setValue("fixedLatitude", lat);
        setValue("fixedLongitude", long);

        var latIsBasicallyNull = fixedLatitude == null || fixedLatitude == 0;
        var longIsBasicallyNull = fixedLongitude == null || fixedLongitude == 0;
        if (latIsBasicallyNull || longIsBasicallyNull) {
            fixedLatitude = null;
            fixedLongitude = null;
            if (clearRequests) {
                clearPendingWebRequests(); // we want the new position to render faster, that might be the same position, which is fine they queue up pretty quick
            }
            updateCachedValues();
            return;
        }

        // we should have a lat and a long at this point
        // updateCachedValues(); already called by the above sets
        // var latlong = RectangularPoint.xyToLatLon(fixedPosition.x, fixedPosition.y);
        // logT("round trip conversion result: " + latlong);
        if (clearRequests) {
            clearPendingWebRequests(); // we want the new position to render faster, that might be the same position, which is fine they queue up pretty quick
        }
    }

    function setFixedPositionWithoutUpdate(
        lat as Float?,
        long as Float?,
        clearRequests as Boolean
    ) as Void {
        // logT("moving to: " + lat + " " + long);
        // be very careful about putting null into properties, it breaks everything
        if (lat == null || !(lat instanceof Float)) {
            lat = 0f;
        }
        if (long == null || !(long instanceof Float)) {
            long = 0f;
        }
        fixedLatitude = lat;
        fixedLongitude = long;
        Application.Properties.setValue("fixedLatitude", lat);
        Application.Properties.setValue("fixedLongitude", long);

        var latIsBasicallyNull = fixedLatitude == null || fixedLatitude == 0;
        var longIsBasicallyNull = fixedLongitude == null || fixedLongitude == 0;
        if (latIsBasicallyNull || longIsBasicallyNull) {
            fixedLatitude = null;
            fixedLongitude = null;
            if (clearRequests) {
                clearPendingWebRequests(); // we want the new position to render faster, that might be the same position, which is fine they queue up pretty quick
            }
            updateCachedValues();
            return;
        }

        // we should have a lat and a long at this point
        // updateCachedValues(); already called by the above sets
        // var latlong = RectangularPoint.xyToLatLon(fixedPosition.x, fixedPosition.y);
        // logT("round trip conversion result: " + latlong);
        if (clearRequests) {
            clearPendingWebRequests(); // we want the new position to render faster, that might be the same position, which is fine they queue up pretty quick
        }
    }

    function setValue(key as String, value as PropertyValueType) as Void {
        Application.Properties.setValue(key, value);
        setValueSideEffect();
    }

    function setValueSideEffect() as Void {
        updateCachedValues();
        updateViewSettings();
        updateRouteSettings();
    }

    function setZoomAtPaceMode(_zoomAtPaceMode as Number) as Void {
        zoomAtPaceMode = _zoomAtPaceMode;
        setValue("zoomAtPaceMode", zoomAtPaceMode);
    }

    (:settingsView)
    function setMapChoice(value as Number) as Void {
        setMapChoiceWithoutSideEffect(value);
        setValueSideEffect();
        mapChoiceChanged();
    }

    function setMapChoiceWithoutSideEffect(value as Number) as Void {
        mapChoice = value;
        Application.Properties.setValue("mapChoice", mapChoice);
    }

    function mapChoiceChanged() as Void {
        updateMapChoiceChange(mapChoice);
        updateCachedValues();
        updateViewSettings();
    }

    function authMissing() as Boolean {
        return requiresAuth && authToken.equals("");
    }

    (:noCompanionTiles)
    function mapChoiceValid() as Boolean {
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) != null) {
            return false;
        }

        return mapChoice == 0 /*custom*/ || getTileServerInfo(mapChoice) != null;
    }

    (:companionTiles)
    function mapChoiceValid() as Boolean {
        return (
            mapChoice == 0 /*custom*/ ||
            mapChoice == 1 /*companion*/ ||
            getTileServerInfo(mapChoice) != null
        );
    }

    (:noImageTiles)
    function getAttribution() as WatchUi.BitmapResource? {
        return null;
    }

    (:imageTiles)
    function getAttribution() as WatchUi.BitmapResource? {
        if (mapChoice == 0) {
            // custom - no way to know which tile server
            return null;
        } else if (mapChoice == 1) {
            // companion app - attributions in the companion app (no way to know what image tiles we are getting)
            return null;
        }

        var tileServerInfo = getTileServerInfo(mapChoice);
        if (tileServerInfo == null) {
            return null; // invalid selection
        }

        if (tileServerInfo.attributionType != attributionImageType) {
            switch (tileServerInfo.attributionType) {
                case ATTRIBUTION_OPENTOPOMAP:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.OpenTopMapAttribution) as BitmapResource;
                    break;
                case ATTRIBUTION_ESRI:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.EsriAttribution) as BitmapResource;
                    break;
                case ATTRIBUTION_OPENSTREETMAP:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.OpenStreetMapAttribution) as
                        BitmapResource;
                    break;
                case ATTRIBUTION_STADIA:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.StadiaAttribution) as BitmapResource;
                    break;
                case ATTRIBUTION_CARTO:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.CartoAttribution) as BitmapResource;
                    break;
                case ATTRIBUTION_MAPY:
                    attributionImage =
                        WatchUi.loadResource(Rez.Drawables.MapyAttribution) as BitmapResource;
                    break;
            }
        }

        attributionImageType = tileServerInfo.attributionType;
        return attributionImage;
    }

    (:lowMemory)
    function maxTileCacheSizeGuess() as Number {
        return 8; // its in graphics memory, so it `should` be fine
    }

    // this is a wild guess, its only used to try and protect users
    // they can set it higher after configuring the tile server choice, or custom mode is full hands off
    // this is just to ry and limit it for users when they ar simply selecting a new map choice
    (:highMemory)
    function maxTileCacheSizeGuess() as Number {
        // this guess is off for large memeory devices, because the graphics memory pool is the limitation, and it has no relation to the standard app memory pool
        // so hard coding guess instead
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) != null) {
            // companion app is normally 64*64, thats the largest tile they can have without OOM, so cap at a reasonable value
            return 64;
        }

        return 10;
    }

    function maxStorageTileCacheSizeGuess() as Number {
        // StorageTileCache._tilesInStorage is the limiting factor for companion app tiles when using TILE_DATA_TYPE_BASE64_FULL_COLOUR
        // but storage size is the limiting factor for 192*192 image tiles
        // since there is no way to know if we are using TILE_DATA_TYPE_BASE64_FULL_COLOUR or some other mode we will just assume its that
        // see notes above on storageTileCacheSize variable
        return maxTileCacheSizeGuess() * 4; // this will result in ~64 for image tiles and ~324 for companion app 64*64 tiles
    }

    (:noCompanionTiles)
    function updateCompanionAppMapChoiceChange() as Void {}
    (:companionTiles)
    function updateCompanionAppMapChoiceChange() as Void {
        // setting back to defaults otherwise when we chose companion app we will not get the correct tilesize and it will crash
        var defaultSettings = new Settings();
        // we want a tile layer that all tile servers should be able to show, it should also be low enough that users see there is a problem
        // these values will be updated by companion app when tile server changes, or the query below
        setTileLayerMaxWithoutSideEffect(8);
        setTileLayerMinWithoutSideEffect(0);
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) == null) {
            setTileUrlWithoutSideEffect(COMPANION_APP_TILE_URL);
        }
        if (fullTileSize != defaultSettings.fullTileSize) {
            setFullTileSizeWithoutSideEffect(defaultSettings.fullTileSize);
        }
        if (scaledTileSize != defaultSettings.scaledTileSize) {
            setScaledTileSizeWithoutSideEffect(defaultSettings.scaledTileSize);
        }
        if (tileSize != defaultSettings.tileSize) {
            setTileSizeWithoutSideEffect(defaultSettings.tileSize);
        }
        var tileCacheMax = maxTileCacheSizeGuess();
        if (tileCacheSize > tileCacheMax) {
            logD("limiting tile cache size to: " + tileCacheMax);
            setTileCacheSizeWithoutSideEffect(tileCacheMax);
        }
        var storageTileCacheSizeMax = maxStorageTileCacheSizeGuess();
        if (storageTileCacheSize > storageTileCacheSizeMax) {
            logD("limiting storage tile cache size to: " + storageTileCacheSizeMax);
            setStorageTileCacheSizeWithoutSideEffect(storageTileCacheSizeMax);
        }

        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }

        // grab the min and max from the tile server
        _breadcrumbContextLocal.webRequestHandler.addHighPriority(
            new JsonRequest(
                "TUH-" + mapChoiceVersion,
                tileUrl + "/tileServerDetails",
                {},
                new TileUpdateHandler(mapChoiceVersion)
            )
        );

        return;
    }

    (:noImageTiles)
    function updateTileServerMapChoiceChange(tileServerInfo as TileServerInfo) as Void {}

    (:imageTiles)
    function updateTileServerMapChoiceChange(tileServerInfo as TileServerInfo) as Void {
        var defaultSettings = new Settings();
        // auth token added later
        var newUrl =
            getUrlPrefix(tileServerInfo.urlPrefix) +
            tileServerInfo.urlTemplate +
            getAuthTokenSuffix(tileServerInfo.authTokenType);
        if (!tileUrl.equals(newUrl)) {
            // set url last to clear tile cache (if needed)
            setTileUrlWithoutSideEffect(newUrl);
        }
        if (tileLayerMax != tileServerInfo.tileLayerMax) {
            setTileLayerMaxWithoutSideEffect(tileServerInfo.tileLayerMax);
        }
        if (tileLayerMin != tileServerInfo.tileLayerMin) {
            setTileLayerMinWithoutSideEffect(tileServerInfo.tileLayerMin);
        }
        if (fullTileSize != 256) {
            setFullTileSizeWithoutSideEffect(256);
        }
        // todo: reduce this to 128 for better results
        if (scaledTileSize != defaultSettings.scaledTileSize) {
            setScaledTileSizeWithoutSideEffect(defaultSettings.scaledTileSize);
        }
        if (tileSize != defaultSettings.scaledTileSize) {
            setTileSizeWithoutSideEffect(defaultSettings.scaledTileSize);
        }
        var tileCacheMax = maxTileCacheSizeGuess();
        if (tileCacheSize > tileCacheMax) {
            logD("limiting tile cache size to: " + tileCacheMax);
            setTileCacheSizeWithoutSideEffect(tileCacheMax);
        }
        var storageTileCacheSizeMax = maxStorageTileCacheSizeGuess();
        if (storageTileCacheSize > storageTileCacheSizeMax) {
            logD("limiting storage tile cache size to: " + storageTileCacheSizeMax);
            setStorageTileCacheSizeWithoutSideEffect(storageTileCacheSizeMax);
        }
    }

    function companionChangedToMaxMin(minLayer as Number, maxLayer as Number) as Void {
        // we need to to force the url to be companion app
        // update to be custom (since companion app url will override tile layers)
        // This does mean when the users selects companion app on the watch settings it might not match the currently
        // configured tile server max/min on the companion app
        // assert(tileUrl.equals(COMPANION_APP_TILE_URL));

        // if the users goes custom and has the companion app url, we still do not update the tiles layers
        // this is because they may be artificially capping the tileLayerMax property eg. a tile server on the phone that has 20 layers, but the user only wants
        // 15 layers on the watch in order to be able to run offline tiles and store them all.
        if (mapChoice != 1) {
            // we are no longer on the companion app, abort
            return;
        }

        setTileLayerMaxWithoutSideEffect(maxLayer);
        setTileLayerMinWithoutSideEffect(minLayer);
        setValueSideEffect();
    }

    function updateMapChoiceChange(value as Number) as Void {
        // we need to store the last one we processed seperately so we can know on startup if the setting changed underneath us
        var lastMapChoice = Application.Storage.getValue("lastMapChoice");
        if (lastMapChoice == null || (lastMapChoice instanceof Number and lastMapChoice != value)) {
            updateMapChoiceChangeInner(value);
        }
        safeSetStorage("lastMapChoice", value);
    }

    function updateMapChoiceChangeInner(value as Number) as Void {
        ++mapChoiceVersion;

        if (value == 0) {
            // custom - leave everything alone
            return;
        } else if (value == 1) {
            // companion app
            updateCompanionAppMapChoiceChange();
            return;
        }

        var tileServerInfo = getTileServerInfo(value);
        if (tileServerInfo == null) {
            return; // invalid selection
        }
        updateTileServerMapChoiceChange(tileServerInfo);
    }

    (:settingsView)
    function setTileUrl(_tileUrl as String) as Void {
        setTileUrlWithoutSideEffect(_tileUrl);
        setValueSideEffect();
    }

    function setTileUrlWithoutSideEffect(_tileUrl as String) as Void {
        tileUrl = _tileUrl;
        Application.Properties.setValue("tileUrl", tileUrl);
        tileUrlChanged();
    }

    function tileUrlChanged() as Void {
        clearPendingWebRequests();
        clearTileCache();
        clearTileCacheStats();
        clearWebStats();
        updateRequiresAuth();

        // prompts user to open the app
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) != null && !storageMapTilesOnly) {
            // we could also send a toast, but the transmit allows us to open the app easier on the phone
            // even though the phone side is a bit of a hack (ConnectIQMessageReceiver cannot parse the data), it's still better than having to manualy open the app.
            transmit([PROTOCOL_SEND_OPEN_APP], {}, new CommStatus());
        }
    }

    function updateRequiresAuth() as Void {
        requiresAuth = tileUrl.find("{authToken}") != null;
    }

    (:settingsView)
    function setAuthToken(value as String) as Void {
        authToken = value;
        setValue("authToken", authToken);
        tileServerPropChanged();
    }

    (:settingsView)
    function setZoomAtPaceSpeedMPS(mps as Float) as Void {
        zoomAtPaceSpeedMPS = mps;
        setValue("zoomAtPaceSpeedMPS", zoomAtPaceSpeedMPS);
    }

    (:settingsView)
    function setUseTrackAsHeadingSpeedMPS(mps as Float) as Void {
        useTrackAsHeadingSpeedMPS = mps;
        setValue("useTrackAsHeadingSpeedMPS", useTrackAsHeadingSpeedMPS);
    }

    (:settingsView)
    function setMetersAroundUser(value as Number) as Void {
        metersAroundUser = value;
        setValue("metersAroundUser", metersAroundUser);
    }

    (:settingsView)
    function setTopDataType(value as Number) as Void {
        topDataType = value;
        setValue("topDataType", topDataType);
    }

    (:settingsView)
    function setBottomDataType(value as Number) as Void {
        bottomDataType = value;
        setValue("bottomDataType", bottomDataType);
    }

    (:settingsView)
    function setAutoLapDistanceM(value as Number) as Void {
        autoLapDistanceM = value;
        setValue("autoLapDistanceM", autoLapDistanceM);
    }
    
    (:settingsView)
    function setMinTrackPointDistanceM(value as Number) as Void {
        minTrackPointDistanceM = value;
        setValue("minTrackPointDistanceM", minTrackPointDistanceM);
        setMinTrackPointDistanceMSideEffect();
    }

    function setMinTrackPointDistanceMSideEffect() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        // only the track needs the setting, routes do not matter, they can stay at the default (5m) because they are limited by the companion app anyway
        _breadcrumbContextLocal.track.setMinDistanceM(
            minTrackPointDistanceM.toFloat(),
            _breadcrumbContextLocal.cachedValues.currentScale
        );
    }

    (:settingsView)
    function setTrackPointReductionMethod(value as Number) as Void {
        trackPointReductionMethod = value;
        setValue("trackPointReductionMethod", trackPointReductionMethod);
    }

    (:settingsView)
    function setDataFieldTextSize(value as Number) as Void {
        dataFieldTextSize = value;
        setValue("dataFieldTextSize", dataFieldTextSize);
    }

    (:settingsView)
    function getOffsetForPage(pageIndex as Number) as Number {
        var offset = 0;
        for (var i = 0; i < pageIndex; i++) {
            offset += dataFieldPageCounts[i];
        }
        return offset;
    }

    (:settingsView)
    function setPageFieldType(
        pageIndex as Number,
        fieldIndex as Number,
        newType as Number
    ) as Void {
        var offset = getOffsetForPage(pageIndex);
        var targetIndex = offset + fieldIndex;

        if (targetIndex < dataFieldPageTypes.size()) {
            dataFieldPageTypes[targetIndex] = newType;
        }

        saveDataFieldPages();
    }

    (:settingsView)
    function getFieldType(pageIndex as Number, fieldIndex as Number) as Number {
        var offset = getOffsetForPage(pageIndex);
        var targetIndex = offset + fieldIndex;

        // Bounds check to ensure we don't crash
        if (targetIndex < dataFieldPageTypes.size()) {
            return dataFieldPageTypes[targetIndex];
        }
        return 0; // Default or error case
    }

    (:settingsView)
    function getTypesForPage(pageIndex as Number) as Array<Number> {
        var offset = getOffsetForPage(pageIndex);
        var count = dataFieldPageCounts[pageIndex];
        var pageTypes = [];

        for (var i = 0; i < count; i++) {
            pageTypes.add(dataFieldPageTypes[offset + i] as Number);
        }
        return pageTypes as Array<Number>;
    }

    (:settingsView)
    function addNewPage() as Number {
        dataFieldPageCounts.add(1);
        dataFieldPageTypes.add(DATA_TYPE_NONE);
        saveDataFieldPages();
        var newPageIndex = dataFieldPageCounts.size() - 1;
        var newModeId = DATA_PAGE_BASE_ID + newPageIndex;

        // Now also add to the display order
        if (modeDisplayOrder.indexOf(newModeId) == -1) {
            modeDisplayOrder.add(newModeId);
        }
        setValue("modeDisplayOrder", encodeCSV(modeDisplayOrder));

        return newPageIndex;
    }

    (:settingsView)
    function removePage(pageIndex as Number) as Void {
        if (pageIndex < 0 || pageIndex >= dataFieldPageCounts.size()) {
            return;
        }

        // 1. Get info BEFORE we mutate the page counts array
        var offset = getOffsetForPage(pageIndex);
        var countToRemove = dataFieldPageCounts[pageIndex];

        // 2. Remove fields from the flattened types array
        // We slice the parts before and after the page's block of fields
        var before = dataFieldPageTypes.slice(0, offset);
        var after = dataFieldPageTypes.slice(offset + countToRemove, null);

        // Concatenate to remove the target block
        before.addAll(after);
        dataFieldPageTypes = before;

        // 3. REMOVE BY INDEX from dataFieldPageCounts
        dataFieldPageCounts = removeAtIndex(dataFieldPageCounts, pageIndex);

        // 4. Update modeDisplayOrder
        var pageId = DATA_PAGE_BASE_ID + pageIndex;

        // Remove the page ID from the display order
        modeDisplayOrder.remove(pageId);

        // 5. Re-index remaining page IDs in modeDisplayOrder
        // Any DataPage ID greater than the removed one must be decremented
        for (var i = 0; i < modeDisplayOrder.size(); i++) {
            var currentId = modeDisplayOrder[i];
            if (currentId > pageId) {
                modeDisplayOrder[i] = currentId - 1;
            }
        }

        saveDataFieldPages();
        setValue("modeDisplayOrder", encodeCSV(modeDisplayOrder));
    }

    (:settingsView)
    function addNewField(pageIndex as Number) as Void {
        var offset = getOffsetForPage(pageIndex);
        var count = dataFieldPageCounts[pageIndex];
        var insertIndex = offset + count;

        var before = dataFieldPageTypes.slice(0, insertIndex);
        var after = dataFieldPageTypes.slice(insertIndex, null);

        var newArray = before;
        newArray.add(DATA_TYPE_NONE);
        newArray.addAll(after);

        dataFieldPageTypes = newArray;
        dataFieldPageCounts[pageIndex] = count + 1;
        saveDataFieldPages();
    }

    (:settingsView)
    function removeField(pageIndex as Number, fieldIndex as Number) as Void {
        if (dataFieldPageCounts[pageIndex] <= 0) {
            return; // dno how this happened, but try and prrevent it going negative
        }

        var offset = 0;
        for (var i = 0; i < pageIndex; i++) {
            offset += dataFieldPageCounts[i];
        }

        dataFieldPageTypes = removeAtIndex(dataFieldPageTypes, offset + fieldIndex);
        dataFieldPageCounts[pageIndex] = dataFieldPageCounts[pageIndex] - 1;
        saveDataFieldPages();
    }

    (:settingsView)
    function validateDataFieldPages() as Void {
        var expectedTotalFields = 0;
        for (var i = 0; i < dataFieldPageCounts.size(); i++) {
            expectedTotalFields += dataFieldPageCounts[i];
        }

        var actualTotalFields = dataFieldPageTypes.size();

        if (actualTotalFields < expectedTotalFields) {
            // We are missing fields; pad with NONE
            var missingCount = expectedTotalFields - actualTotalFields;
            for (var i = 0; i < missingCount; i++) {
                dataFieldPageTypes.add(DATA_TYPE_NONE);
            }
        } else if (actualTotalFields > expectedTotalFields) {
            // We have too many fields; slice them off
            dataFieldPageTypes = dataFieldPageTypes.slice(0, expectedTotalFields);
        }

        // 1. Remove orphaned Data Page IDs from modeDisplayOrder
        for (var i = modeDisplayOrder.size() - 1; i >= 0; i--) {
            var val = modeDisplayOrder[i];
            if (val >= DATA_PAGE_BASE_ID) {
                var pageIndex = val - DATA_PAGE_BASE_ID;
                if (pageIndex >= dataFieldPageCounts.size()) {
                    modeDisplayOrder.remove(val);
                }
            }
        }

        // 2. Ensure every existing page has an entry
        for (var i = 0; i < dataFieldPageCounts.size(); i++) {
            var pageId = DATA_PAGE_BASE_ID + i;
            if (modeDisplayOrder.indexOf(pageId) == -1) {
                modeDisplayOrder.add(pageId);
            }
        }
    }

    (:settingsView)
    function saveDataFieldPages() as Void {
        validateDataFieldPages();
        Application.Properties.setValue("dataFieldPageTypes", encodeCSV(dataFieldPageTypes));
        Application.Properties.setValue("dataFieldPageCounts", encodeCSV(dataFieldPageCounts));
    }

    (:settingsView)
    function setFixedLatitude(value as Float) as Void {
        setFixedPosition(value, fixedLongitude, true);
    }

    (:settingsView)
    function setFixedLongitude(value as Float) as Void {
        setFixedPosition(fixedLatitude, value, true);
    }

    (:settingsView)
    function setMaxPendingWebRequests(value as Number) as Void {
        maxPendingWebRequests = value;
        setValue("maxPendingWebRequests", maxPendingWebRequests);
    }

    (:settingsView)
    function setTileSize(value as Number) as Void {
        setTileSizeWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setTileSizeWithoutSideEffect(value as Number) as Void {
        tileSize = value;
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) == null) {
            tileSize = scaledTileSize;
        }
        Application.Properties.setValue("tileSize", tileSize);
        tileServerPropChanged();
    }

    function tileServerPropChanged() as Void {
        clearPendingWebRequests();
        clearTileCache();
    }

    (:settingsView)
    function setHttpErrorTileTTLS(value as Number) as Void {
        httpErrorTileTTLS = value;
        setValue("httpErrorTileTTLS", httpErrorTileTTLS);
        tileServerPropChanged();
    }

    (:settingsView)
    function setTurnAlertTimeS(value as Number) as Void {
        turnAlertTimeS = value;
        setValue("turnAlertTimeS", turnAlertTimeS);
    }

    (:settingsView)
    function setMinTurnAlertDistanceM(value as Number) as Void {
        minTurnAlertDistanceM = value;
        setValue("minTurnAlertDistanceM", minTurnAlertDistanceM);
    }

    (:settingsView)
    function setModeDisplayOrder(value as String) as Void {
        // try and validate it before saving the setting
        modeDisplayOrder = parseCSVStringRaw(
            "modeDisplayOrder",
            value,
            modeDisplayOrder,
            method(:defaultNumberParser)
        );
        setValue("modeDisplayOrder", encodeCSV(modeDisplayOrder));
    }

    (:settingsView)
    function setMaxTrackPoints(value as Number) as Void {
        var oldmaxTrackPoints = maxTrackPoints;
        maxTrackPoints = value;
        if (oldmaxTrackPoints != maxTrackPoints) {
            maxTrackPointsChanged();
        }
        setValue("maxTrackPoints", maxTrackPoints);
    }

    (:settingsView)
    function setTrackStyle(value as Number) as Void {
        trackStyle = value;
        setValue("trackStyle", trackStyle);
        recomputeTrackTexture();
    }

    (:settingsView)
    function setTrackWidth(value as Number) as Void {
        trackWidth = value;
        setValue("trackWidth", trackWidth);
        recomputeTrackTexture();
    }

    function maxTrackPointsChanged() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.track.coordinates.restrictPointsToMaxMemory(
            maxTrackPoints,
            _breadcrumbContextLocal.settings.trackPointReductionMethod,
            _breadcrumbContextLocal.cachedValues.currentScale
        );
    }

    (:settingsView)
    function setShowDirectionPointTextUnderIndex(value as Number) as Void {
        showDirectionPointTextUnderIndex = value;
        setValue("showDirectionPointTextUnderIndex", showDirectionPointTextUnderIndex);
    }

    (:settingsView)
    function setErrorTileTTLS(value as Number) as Void {
        errorTileTTLS = value;
        setValue("errorTileTTLS", errorTileTTLS);
        tileServerPropChanged();
    }

    (:settingsView)
    function setFullTileSize(value as Number) as Void {
        setFullTileSizeWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setFullTileSizeWithoutSideEffect(value as Number) as Void {
        fullTileSize = value;
        Application.Properties.setValue("fullTileSize", fullTileSize);
        tileServerPropChanged();
    }

    (:settingsView)
    function setScaledTileSize(value as Number) as Void {
        setScaledTileSizeWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setScaledTileSizeWithoutSideEffect(value as Number) as Void {
        scaledTileSize = value;
        if (useDrawBitmap) {
            scaledTileSize = fullTileSize;
        }
        Application.Properties.setValue("scaledTileSize", scaledTileSize);
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) == null) {
            setTileSizeWithoutSideEffect(scaledTileSize);
        }
        tileServerPropChanged();
    }

    (:settingsView)
    function setPackingFormat(value as Number) as Void {
        setPackingFormatWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setPackingFormatWithoutSideEffect(value as Number) as Void {
        packingFormat = value;
        Application.Properties.setValue("packingFormat", packingFormat);
        tileServerPropChanged();
    }

    (:settingsView)
    function setUseDrawBitmap(value as Boolean) as Void {
        setUseDrawBitmapWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setUseDrawBitmapWithoutSideEffect(value as Boolean) as Void {
        useDrawBitmap = value;
        Application.Properties.setValue("useDrawBitmap", useDrawBitmap);
        if (useDrawBitmap) {
            setScaledTileSizeWithoutSideEffect(fullTileSize);
        }
        tileServerPropChanged();
    }

    (:settingsView)
    function setTileLayerMax(value as Number) as Void {
        setTileLayerMaxWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setTileLayerMaxWithoutSideEffect(value as Number) as Void {
        tileLayerMax = value;
        Application.Properties.setValue("tileLayerMax", tileLayerMax);
    }

    (:settingsView)
    function setTileLayerMin(value as Number) as Void {
        setTileLayerMinWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setTileLayerMinWithoutSideEffect(value as Number) as Void {
        tileLayerMin = value;
        Application.Properties.setValue("tileLayerMin", tileLayerMin);
    }

    (:settingsView)
    function setDisableMapsFailureCount(value as Number) as Void {
        disableMapsFailureCount = value;
        setValue("disableMapsFailureCount", disableMapsFailureCount);
    }

    (:settingsView)
    function setOffTrackAlertsDistanceM(value as Number) as Void {
        offTrackAlertsDistanceM = value;
        setValue("offTrackAlertsDistanceM", offTrackAlertsDistanceM);
        updateViewSettings();
    }

    (:settingsView)
    function setOffTrackAlertsMaxReportIntervalS(value as Number) as Void {
        offTrackAlertsMaxReportIntervalS = value;
        setValue("offTrackAlertsMaxReportIntervalS", offTrackAlertsMaxReportIntervalS);
        updateViewSettings();
    }

    (:settingsView)
    function setOffTrackCheckIntervalS(value as Number) as Void {
        offTrackCheckIntervalS = value;
        setValue("offTrackCheckIntervalS", offTrackCheckIntervalS);
        updateViewSettings();
    }

    (:settingsView)
    function setRouteMax(value as Number) as Void {
        var oldRouteMax = _routeMax;
        _routeMax = value;
        if (oldRouteMax > _routeMax) {
            routeMaxReduced();
        }
        setValue("routeMax", _routeMax);
        updateCachedValues();
        updateViewSettings();
    }

    function routeMaxReduced() as Void {
        // remove the first oes or the last ones? we do not have an age, so just remove the last ones.
        var routesToRemove = [] as Array<Number>;
        for (var i = _routeMax; i < routes.size(); ++i) {
            var oldRouteEntry = routes[i];
            var oldRouteId = oldRouteEntry["routeId"] as Number;
            routesToRemove.add(oldRouteId);
        }
        for (var i = 0; i < routesToRemove.size(); ++i) {
            var routeId = routesToRemove[i];
            clearRouteFromContext(routeId);
            // do not use the clear route helper method, it will stack overflow
            var routeIndex = getRouteIndexById(routeId);
            if (routeIndex == null) {
                continue;
            }
            routes.remove(routes[routeIndex]);
        }

        saveRoutesNoSideEffect();
    }

    (:settingsView)
    function setTileCacheSize(value as Number) as Void {
        setTileCacheSizeWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setTileCacheSizeWithoutSideEffect(value as Number) as Void {
        var oldTileCacheSize = tileCacheSize;
        tileCacheSize = value;
        Application.Properties.setValue("tileCacheSize", tileCacheSize);

        if (oldTileCacheSize > tileCacheSize) {
            // only nuke tile cache if we reduce the number of tiles we can store
            tileCacheSizeReduced();
        }
    }

    function tileCacheSizeReduced() as Void {
        clearPendingWebRequests();
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // do not remove our cached tiles, we only reduced the caches size, so they are still valid
    }

    function storageTileCacheSizeReduced() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache._storageTileCache.clearValues();
    }

    (:settingsView)
    function setStorageTileCacheSize(value as Number) as Void {
        setStorageTileCacheSizeWithoutSideEffect(value);
        setValueSideEffect();
    }
    function setStorageTileCacheSizeWithoutSideEffect(value as Number) as Void {
        var oldStorageTileCacheSize = storageTileCacheSize;
        storageTileCacheSize = value;
        Application.Properties.setValue("storageTileCacheSize", storageTileCacheSize);

        if (oldStorageTileCacheSize > storageTileCacheSize) {
            // only nuke storage tile cache if we reduce the number of tiles we can store
            storageTileCacheSizeReduced();
        }
    }

    (:settingsView)
    function setStorageTileCachePageCount(value as Number) as Void {
        var oldStorageTileCachePageCount = storageTileCachePageCount;
        storageTileCachePageCount = value;
        if (oldStorageTileCachePageCount != storageTileCachePageCount) {
            storageTilePageCountChanged();
        }
        setValue("storageTileCachePageCount", storageTileCachePageCount);
    }

    function storageTilePageCountChanged() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache._storageTileCache.setNewPageCount(
            storageTileCachePageCount
        );
    }

    (:settingsView)
    function setTileCachePadding(value as Number) as Void {
        tileCachePadding = value;
        setValue("tileCachePadding", tileCachePadding);
    }

    (:settingsView)
    function setStorageSeedRouteDistanceM(value as Float) as Void {
        storageSeedRouteDistanceM = value;
        setValue("storageSeedRouteDistanceM", storageSeedRouteDistanceM);
    }

    (:settingsView)
    function setMapMoveScreenSize(value as Float) as Void {
        mapMoveScreenSize = value;
        setValue("mapMoveScreenSize", mapMoveScreenSize);
    }

    (:settingsView)
    function setCenterUserOffsetY(value as Float) as Void {
        centerUserOffsetY = value;
        setValue("centerUserOffsetY", centerUserOffsetY);
    }

    (:settingsView)
    function setRecalculateIntervalS(value as Number) as Void {
        recalculateIntervalS = value;
        recalculateIntervalS = recalculateIntervalS <= 0 ? 1 : recalculateIntervalS;
        setValue("recalculateIntervalS", recalculateIntervalS);
    }

    function setMapEnabled(_mapEnabled as Boolean) as Void {
        setMapEnabledRaw(_mapEnabled);
        setValue("mapEnabled", mapEnabled);
    }

    function setMapEnabledRaw(_mapEnabled as Boolean) as Void {
        mapEnabled = _mapEnabled;
        mapEnabledChanged();
    }

    function mapEnabledChanged() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }

        if (!mapEnabled) {
            _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // do not remove our cached tiles, we might just be temporarily disabling maps
            clearPendingWebRequests();
            clearTileCacheStats();
            clearWebStats();
            return;
        }

        // prompts user to open the app
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) != null && !storageMapTilesOnly) {
            // we could also send a toast, but the transmit allows us to open the app easier on the phone
            // even though the phone side is a bit of a hack (ConnectIQMessageReceiver cannot parse the data), it's still better than having to manualy open the app.
            transmit([PROTOCOL_SEND_OPEN_APP], {}, new CommStatus());
        }
    }

    (:settingsView)
    function setCacheTilesInStorage(value as Boolean) as Void {
        cacheTilesInStorage = value;
        setValue("cacheTilesInStorage", cacheTilesInStorage);

        if (!cacheTilesInStorage) {
            tileServerPropChanged();
        }
    }

    (:settingsView)
    function setStorageMapTilesOnly(value as Boolean) as Void {
        storageMapTilesOnly = value;
        setValue("storageMapTilesOnly", storageMapTilesOnly);
    }

    (:settingsView)
    function setDrawLineToClosestPoint(value as Boolean) as Void {
        drawLineToClosestPoint = value;
        setValue("drawLineToClosestPoint", drawLineToClosestPoint);
        updateViewSettings();
    }

    (:settingsView)
    function setDrawLineToClosestTrack(value as Boolean) as Void {
        drawLineToClosestTrack = value;
        setValue("drawLineToClosestTrack", drawLineToClosestTrack);
    }
    (:settingsView)
    function setShowTileBorders(value as Boolean) as Void {
        showTileBorders = value;
        setValue("showTileBorders", showTileBorders);
    }
    (:settingsView)
    function setShowErrorTileMessages(value as Boolean) as Void {
        showErrorTileMessages = value;
        setValue("showErrorTileMessages", showErrorTileMessages);
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // remove the errored tiles from cache and redraw
    }
    (:settingsView)
    function setDrawHitBoxes(value as Boolean) as Void {
        drawHitBoxes = value;
        setValue("drawHitBoxes", drawHitBoxes);
    }

    (:settingsView)
    function setShowDirectionPoints(value as Boolean) as Void {
        showDirectionPoints = value;
        setValue("showDirectionPoints", showDirectionPoints);
    }

    (:settingsView)
    function setDisplayLatLong(value as Boolean) as Void {
        displayLatLong = value;
        setValue("displayLatLong", displayLatLong);
    }

    (:settingsView)
    function setUseStartForStop(value as Boolean) as Void {
        useStartForStop = value;
        setValue("useStartForStop", useStartForStop);
        useStartForStopSideEffect();
    }

    function useStartForStopSideEffect() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.breadcrumbRenderer.setCornerPositions();
    }

    function scaleRestrictedToTileLayers() as Boolean {
        return _scaleRestrictedToTileLayers || useDrawBitmap;
    }

    (:settingsView)
    function setDisplayRouteNames(_displayRouteNames as Boolean) as Void {
        displayRouteNames = _displayRouteNames;
        setValue("displayRouteNames", displayRouteNames);
    }

    function setRoutesEnabled(_routesEnabled as Boolean) as Void {
        routesEnabled = _routesEnabled;
        setValue("routesEnabled", routesEnabled);
    }

    function routeColour(routeId as Number) as Number {
        return routeProp(routeId, "colour", defaultRouteColour) as Number;
    }

    function routeColour2(routeId as Number) as Number {
        return routeProp(routeId, "colour2", DEFAULT_ROUTE_COLOUR_2) as Number;
    }

    // see oddity with route name and route loading new in context.newRoute
    function routeName(routeId as Number) as String {
        return routeProp(routeId, "name", "") as String;
    }

    function routeProp(
        routeId as Number,
        key as String,
        defaultVal as Number or String or Boolean
    ) as Number or String or Boolean {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return defaultVal;
        }

        return routes[routeIndex][key] as Number or String or Boolean;
    }

    (:storage)
    function atLeast1RouteEnabled() as Boolean {
        if (!routesEnabled) {
            return false;
        }

        for (var i = 0; i < routes.size(); ++i) {
            var route = routes[i];
            if (route["enabled"]) {
                return true;
            }
        }

        return false;
    }

    function routeEnabled(routeId as Number) as Boolean {
        if (!routesEnabled) {
            return false;
        }

        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return false;
        }
        return routes[routeIndex]["enabled"] as Boolean;
    }

    function routeReversed(routeId as Number) as Boolean {
        return routeProp(routeId, "reversed", false) as Boolean;
    }

    function routeStyle(routeId as Number) as Number {
        return routeProp(routeId, "style", DEFAULT_ROUTE_STYLE) as Number;
    }

    function routeTexture(routeId as Number) as Graphics.BitmapTexture or Number {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return -1;
        }
        padRouteTextures(routeIndex);
        return routeTextures[routeIndex];
    }

    function routeWidth(routeId as Number) as Number {
        return routeProp(routeId, "width", DEFAULT_ROUTE_WIDTH) as Number;
    }

    function ensureDefaultRoute(routeId as Number, name as String) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex != null) {
            routes[routeIndex] = defaultRoute(routeId, name);
            routeTextures[routeIndex] = -1;
            saveRoutes();
            return;
        }

        if (routes.size() >= _routeMax) {
            return;
        }

        routes.add(defaultRoute(routeId, name));
        routeTextures.add(-1);
        saveRoutes();
    }

    function simpleRouteProp(routeId as Number, key as String, value as Number or Boolean) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return;
        }

        routes[routeIndex][key] = value;
        saveRoutes();
        recomputeRouteTexture(routeIndex);
    }

    function setRouteColour(routeId as Number, value as Number) as Void {
        simpleRouteProp(routeId, "colour", value);
    }

    function setRouteColour2(routeId as Number, value as Number) as Void {
        simpleRouteProp(routeId, "colour2", value);
    }

    // see oddity with route name and route loading new in context.newRoute
    function setRouteName(routeId as Number, value as String) as Void {
        setRouteNameNoSideEffect(routeId, value);
        setValueSideEffect();
    }

    function setRouteNameNoSideEffect(routeId as Number, value as String) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return;
        }

        routes[routeIndex]["name"] = value;
        saveRoutesNoSideEffect();
    }

    function setRouteStyle(routeId as Number, value as Number) as Void {
        simpleRouteProp(routeId, "style", value);
    }

    function setRouteWidth(routeId as Number, value as Number) as Void {
        simpleRouteProp(routeId, "width", value);
    }

    function setRouteEnabled(routeId as Number, value as Boolean) as Void {
        simpleRouteProp(routeId, "enabled", value);
    }

    function setRouteReversed(routeId as Number, value as Boolean) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return;
        }

        var oldVal = routes[routeIndex]["reversed"];
        if (oldVal != value) {
            var _breadcrumbContextLocal = $._breadcrumbContext;
            if (_breadcrumbContextLocal != null) {
                _breadcrumbContextLocal.reverseRouteId(routeId);
            }
        }
        routes[routeIndex]["reversed"] = value;
        saveRoutes();
        updateViewSettings();
    }

    function ensureRouteId(routeId as Number) as Void {
        ensureRouteIdNoSideEffect(routeId);
        setValueSideEffect();
    }

    function ensureRouteIdNoSideEffect(routeId as Number) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex != null) {
            return;
        }

        if (routes.size() >= _routeMax) {
            return;
        }

        routes.add(defaultRoute(routeId, ""));
        routeTextures.add(-1);
        saveRoutesNoSideEffect();
    }

    function defaultRoute(routeId as Number, name as String) as Dictionary {
        return {
            "routeId" => routeId,
            "name" => name,
            "enabled" => true,
            "colour" => defaultRouteColour,
            "colour2" => DEFAULT_ROUTE_COLOUR_2,
            "reversed" => false,
            "style" => DEFAULT_ROUTE_STYLE,
            "width" => DEFAULT_ROUTE_WIDTH,
        };
    }

    function getRouteIndexById(routeId as Number) as Number? {
        for (var i = 0; i < routes.size(); ++i) {
            var route = routes[i];
            if (route["routeId"] == routeId) {
                return i;
            }
        }

        return null;
    }

    function clearRoutes() as Void {
        routes = [];
        routeTextures = [];
        saveRoutes();
    }

    function storageCleared() as Void {
        // routes are already cleared seperately through context
        safeSetStorage("lastMapChoice", mapChoice); // make sure we do not reload our map choice
    }

    function clearRoute(routeId as Number) as Void {
        var routeIndex = getRouteIndexById(routeId);
        if (routeIndex == null) {
            return;
        }
        routes.remove(routes[routeIndex]);
        routeTextures.remove(routeTextures[routeIndex]);
        saveRoutes();
    }

    function routesToSave() as Array<Dictionary<String, PropertyValueType> > {
        var toSave = [] as Array<Dictionary<String, PropertyValueType> >;
        for (var i = 0; i < routes.size(); ++i) {
            var entry = routes[i];
            var toAdd =
                ({
                    "routeId" => entry["routeId"] as Number,
                    "name" => entry["name"] as String,
                    "enabled" => entry["enabled"] as Boolean,
                    "colour" => (entry["colour"] as Number).format("%X"), // this is why we have to copy it :(
                    "colour2" => (entry["colour2"] as Number).format("%X"), // this is why we have to copy it :(
                    "reversed" => entry["reversed"] as Boolean,
                    "style" => entry["style"] as Number,
                    "width" => entry["width"] as Number,
                }) as Dictionary<String, PropertyValueType>;
            toSave.add(toAdd);
        }
        return toSave;
    }

    function saveRoutes() as Void {
        saveRoutesNoSideEffect();
        setValueSideEffect();
    }

    function saveRoutesNoSideEffect() as Void {
        var toSave = routesToSave();
        // note toSave is Array<Dictionary<String, PropertyValueType>>
        // but the compiler only allows "Array<PropertyValueType>" even though the array of dicts seems to work on sim and real watch
        safeSetStorage("routes", toSave as Array<PropertyValueType>);
    }

    (:settingsView)
    function setTrackColour(value as Number) as Void {
        trackColour = value;
        setValue("trackColour", trackColour.format("%X"));
        recomputeTrackTexture();
    }

    (:settingsView)
    function setTrackColour2(value as Number) as Void {
        trackColour2 = value;
        setValue("trackColour2", trackColour2.format("%X"));
        recomputeTrackTexture();
    }

    function recomputeTrackTexture() as Void {
        trackTexture = getTexture(
            trackStyle,
            trackWidth,
            trackWidth / 2,
            trackColour,
            trackColour2
        );
    }

    function recomputeRouteTexture(routeIndex as Number) as Void {
        padRouteTextures(routeIndex);
        var route = routes[routeIndex];
        var currentWidth = route["width"] as Number;
        routeTextures[routeIndex] = getTexture(
            route["style"] as Number,
            currentWidth,
            currentWidth / 2,
            route["colour"] as Number,
            route["colour2"] as Number
        );
    }

    function padRouteTextures(routeIndex as Number) as Void {
        if (routeTextures.size() <= routeIndex) {
            // Calculate how many new slots we need
            var elementsToAdd = routeIndex + 1 - routeTextures.size();

            // Create the "padding" array filled with -1
            var padding = new [elementsToAdd] as Array<Graphics.BitmapTexture or Number>;
            for (var i = 0; i < elementsToAdd; i++) {
                padding[i] = -1;
            }

            routeTextures.addAll(padding);
        }
    }

    (:settingsView)
    function setDefaultRouteColour(value as Number) as Void {
        defaultRouteColour = value;
        setValue("defaultRouteColour", defaultRouteColour.format("%X"));
    }

    (:settingsView)
    function setTileErrorColour(value as Number) as Void {
        tileErrorColour = value;
        setValue("tileErrorColour", tileErrorColour.format("%X"));
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // remove the errored tiles from cache and redraw
    }

    (:settingsView)
    function setUserColour(value as Number) as Void {
        userColour = value;
        setValue("userColour", userColour.format("%X"));
    }

    (:settingsView)
    function setNormalModeColour(value as Number) as Void {
        normalModeColour = value;
        setValue("normalModeColour", normalModeColour.format("%X"));
    }

    (:settingsView)
    function setDebugColour(value as Number) as Void {
        debugColour = value;
        setValue("debugColour", debugColour.format("%X"));
    }

    (:settingsView)
    function setUiColour(value as Number) as Void {
        uiColour = value;
        setValue("uiColour", uiColour.format("%X"));
    }

    (:settingsView)
    function setElevationColour(value as Number) as Void {
        elevationColour = value;
        setValue("elevationColour", elevationColour.format("%X"));
    }

    function toggleMapEnabled() as Void {
        if (mapEnabled) {
            setMapEnabled(false);
            return;
        }

        setMapEnabled(true);
    }

    function toggleUseDrawBitmap() as Void {
        if (useDrawBitmap) {
            setUseDrawBitmapWithoutSideEffect(false);
            tileServerPropChanged();
            setValueSideEffect();
            return;
        }

        setUseDrawBitmapWithoutSideEffect(true);
        tileServerPropChanged();
        setValueSideEffect();
    }

    (:settingsView)
    function toggleStorageSeedBoundingBox() as Void {
        storageSeedBoundingBox = !storageSeedBoundingBox;
        setValue("storageSeedBoundingBox", storageSeedBoundingBox);
    }
    (:settingsView)
    function toggleCacheTilesInStorage() as Void {
        cacheTilesInStorage = !cacheTilesInStorage;
        setValue("cacheTilesInStorage", cacheTilesInStorage);
        tileServerPropChanged();
    }
    (:settingsView)
    function toggleStorageMapTilesOnly() as Void {
        storageMapTilesOnly = !storageMapTilesOnly;
        setValue("storageMapTilesOnly", storageMapTilesOnly);
    }
    (:settingsView)
    function toggleDrawLineToClosestPoint() as Void {
        drawLineToClosestPoint = !drawLineToClosestPoint;
        setValue("drawLineToClosestPoint", drawLineToClosestPoint);
    }
    (:settingsView)
    function toggleDrawLineToClosestTrack() as Void {
        drawLineToClosestTrack = !drawLineToClosestTrack;
        setValue("drawLineToClosestTrack", drawLineToClosestTrack);
    }
    (:settingsView)
    function toggleShowTileBorders() as Void {
        showTileBorders = !showTileBorders;
        setValue("showTileBorders", showTileBorders);
    }
    (:settingsView)
    function toggleShowErrorTileMessages() as Void {
        showErrorTileMessages = !showErrorTileMessages;
        setValue("showErrorTileMessages", showErrorTileMessages);
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // remove the errored tiles from cache and redraw
    }
    (:settingsView)
    function toggleDrawHitBoxes() as Void {
        drawHitBoxes = !drawHitBoxes;
        setValue("drawHitBoxes", drawHitBoxes);
    }
    (:settingsView)
    function toggleShowDirectionPoints() as Void {
        showDirectionPoints = !showDirectionPoints;
        setValue("showDirectionPoints", showDirectionPoints);
    }
    (:settingsView)
    function toggleDisplayLatLong() as Void {
        displayLatLong = !displayLatLong;
        setValue("displayLatLong", displayLatLong);
    }
    (:settingsView)
    function toggleUseStartForStop() as Void {
        useStartForStop = !useStartForStop;
        setValue("useStartForStop", useStartForStop);
        useStartForStopSideEffect();
    }
    (:settingsView)
    function toggleScaleRestrictedToTileLayers() as Void {
        _scaleRestrictedToTileLayers = !_scaleRestrictedToTileLayers;
        setValue("scaleRestrictedToTileLayers", _scaleRestrictedToTileLayers);
    }
    (:settingsView)
    function toggleDisplayRouteNames() as Void {
        displayRouteNames = !displayRouteNames;
        setValue("displayRouteNames", displayRouteNames);
    }
    (:settingsView)
    function toggleEnableOffTrackAlerts() as Void {
        enableOffTrackAlerts = !enableOffTrackAlerts;
        setValue("enableOffTrackAlerts", enableOffTrackAlerts);
    }
    (:settingsView)
    function toggleOffTrackWrongDirection() as Void {
        offTrackWrongDirection = !offTrackWrongDirection;
        setValue("offTrackWrongDirection", offTrackWrongDirection);
    }
    (:settingsView)
    function toggleDrawCheverons() as Void {
        drawCheverons = !drawCheverons;
        setValue("drawCheverons", drawCheverons);
    }
    (:settingsView)
    function toggleRoutesEnabled() as Void {
        routesEnabled = !routesEnabled;
        setValue("routesEnabled", routesEnabled);
    }

    function getNextMode() as Number {
        // does not handle dupes, but thats the user error if they do that
        if (modeDisplayOrder.size() < 1) {
            // they want to stay locked to the current mode thats picked
            return mode;
        }

        var curentModeIndex = modeDisplayOrder.indexOf(mode);
        if (curentModeIndex == -1 || curentModeIndex == modeDisplayOrder.size() - 1) {
            // not found, or we need to go back to the start of the array
            return modeDisplayOrder[0];
        }

        return modeDisplayOrder[curentModeIndex + 1];
    }

    function nextMode() as Void {
        // logT("mode cycled");
        mode = getNextMode();

        // try 5 times to get a good mode, if we can't bail out, better than an infinite while loop
        // helps if users do something like 1,2,3,40,5,6 it will ship over the bad '40' mode
        for (var i = 0; i < 5; ++i) {
            if (
                (mode >= 0 && mode < MODE_MAX) ||
                (dataFieldPageCounts.size() > 0 &&
                    mode >= DATA_PAGE_BASE_ID &&
                    mode < DATA_PAGE_BASE_ID + dataFieldPageCounts.size())
            ) {
                // not the best validation check, but modes are continuous for now
                // if we ever have gaps we will need to check for those too
                break;
            }
            mode = getNextMode();
        }
        setMode(mode);
    }

    function nextZoomAtPaceMode() as Void {
        if (mode != MODE_NORMAL) {
            return;
        }

        // could also do this? not sure what better for perf (probably the modulo 1 less instruction), below is more readable
        // zoomAtPaceMode = (zoomAtPaceMode + 1) % ZOOM_AT_PACE_MODE_MAX;
        zoomAtPaceMode++;
        if (zoomAtPaceMode >= ZOOM_AT_PACE_MODE_MAX) {
            zoomAtPaceMode = ZOOM_AT_PACE_MODE_PACE;
        }

        setZoomAtPaceMode(zoomAtPaceMode);
    }

    function clearTileCache() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearValues();
    }

    function clearStorageTiles() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache._storageTileCache.clearValues();
    }

    function transmit(
        content as Application.PersistableType,
        options as Dictionary?,
        listener as Communications.ConnectionListener
    ) as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.webRequestHandler.transmit(content, options, listener);
    }

    function clearTileCacheStats() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.tileCache.clearStats();
    }

    function clearPendingWebRequests() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.webRequestHandler.clearValues();
    }

    function updateViewSettings() as Void {
        var _viewLocal = $._view;
        if (_viewLocal != null) {
            _viewLocal.onSettingsChanged();
        }
    }

    function updateRouteSettings() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        var contextRoutes = _breadcrumbContextLocal.routes;
        for (var i = 0; i < contextRoutes.size(); ++i) {
            var route = contextRoutes[i];
            // we do not care if its curently disabled, nuke the data anyway
            // if (!routeEnabled(route.storageIndex)) {
            //     continue;
            // }
            // todo only call this if setting sthat effect it changed, taking nuclear approach for now
            route.settingsChanged();
        }
    }

    function updateCachedValues() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.cachedValues.recalculateAll();
    }

    function clearWebStats() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.webRequestHandler.clearStats();
    }

    function clearContextRoutes() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.clearRoutes();
    }

    function clearRouteFromContext(routeId as Number) as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.clearRouteId(routeId);
    }

    function purgeRoutesFromContext() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.purgeRoutes();
    }

    // some times these parserswere throwing when it was an empty strings seem to result in, or wrong type
    //
    // Error: Unhandled Exception
    // Exception: UnexpectedTypeException: Expected Number/Float/Long/Double/Char, given null/Number
    function parseColourTransparency(
        key as String,
        defaultValue as Number,
        allowTransparent as Boolean
    ) as Number {
        try {
            return parseColourRaw(
                key,
                Application.Properties.getValue(key),
                defaultValue,
                allowTransparent
            );
        } catch (e) {
            logE("Error parsing float: " + key);
        }
        return defaultValue;
    }

    function parseColour(key as String, defaultValue as Number) as Number {
        return parseColourTransparency(key, defaultValue, false);
    }

    static function parseColourRaw(
        key as String,
        colourString as PropertyValueType,
        defaultValue as Number,
        allowTransparent as Boolean
    ) as Number {
        try {
            if (colourString == null) {
                return defaultValue;
            }

            if (colourString instanceof String) {
                // want final string as AARRGGBB
                // colourString = padStart(colourString, 6, '0'); // fill in 24 bit colour with 0's
                // colourString = padStart(colourString, 8, 'F'); // pad alpha channel with FF
                // empty or invalid strings convert to null
                // anything with leading FF (when 8 characters supplied) needs to be a long, because its too big to fit in Number
                // if a user chooses FFFFFFFF (white) it is (-1) which is fully transparent, should choose FFFFFF (no alpha) or something close like FFFFFFFE
                // in any case we are currently ignoring alpha because we use setColor (text does not support alpha)
                var long = colourString.toLongWithBase(16);
                if (long == null) {
                    return defaultValue;
                }

                // calling tonumber breaks - because its out of range, but we need to set the alpha bits
                var number = (long & 0xffffffffl).toNumber();
                if (number == 0xffffffff && !allowTransparent) {
                    // -1 is transparent and will not render
                    number = 0xfeffffff;
                }
                return number;
            }

            return parseNumberRaw(key, colourString, defaultValue);
        } catch (e) {
            logE("Error parsing colour: " + key + " " + colourString);
        }
        return defaultValue;
    }

    function parseNumber(key as String, defaultValue as Number) as Number {
        try {
            return parseNumberRaw(key, Application.Properties.getValue(key), defaultValue);
        } catch (e) {
            logE("Error parsing float: " + key);
        }
        return defaultValue;
    }

    static function parseNumberRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as Number
    ) as Number {
        try {
            if (value == null) {
                return defaultValue;
            }

            if (
                value instanceof String ||
                value instanceof Float ||
                value instanceof Number ||
                value instanceof Double
            ) {
                // empty or invalid strings convert to null
                var ret = value.toNumber();
                if (ret == null) {
                    return defaultValue;
                }

                return ret;
            }

            return defaultValue;
        } catch (e) {
            logE("Error parsing number: " + key + " " + value);
        }
        return defaultValue;
    }

    static function encodeCSV(value as Array<ReturnType>) as String {
        var result = "";
        var size = value.size();

        for (var i = 0; i < size; ++i) {
            // Convert element to string (works for both Number and String)
            result += value[i].toString();

            // Add a comma after every element except the last one
            if (i < size - 1) {
                result += ",";
            }
        }

        return result;
    }

    typedef ReturnType as Number /* or String*/;
    function parseCSVString(
        key as String,
        defaultValue as Array<ReturnType>,
        callback as (Method(key as String, value as PropertyValueType) as ReturnType)
    ) as Array<ReturnType> {
        try {
            return parseCSVStringRaw(
                key,
                Application.Properties.getValue(key),
                defaultValue,
                callback
            );
        } catch (e) {
            logE("Error parsing float: " + key);
        }
        return defaultValue;
    }

    function parseCSVStringRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as Array<ReturnType>,
        callback as (Method(key as String, value as PropertyValueType) as ReturnType)
    ) as Array<ReturnType> {
        try {
            if (value == null) {
                return defaultValue;
            }

            if (value instanceof String) {
                var string = value;
                var splitter = ",";
                var result = [] as Array<ReturnType>;
                var location = string.find(splitter) as Number?;

                while (location != null) {
                    result.add(callback.invoke(key, string.substring(0, location) as String));

                    // Truncate the string to look for the next splitter
                    string =
                        string.substring(location + splitter.length(), string.length()) as String;

                    location = string.find(splitter);
                }

                // Add the remaining part of the string if it's not empty
                if (string.length() > 0) {
                    result.add(callback.invoke(key, string));
                }

                return result;
            }

            return defaultValue;
        } catch (e) {
            logE("Error parsing string: " + key + " " + value);
        }
        return defaultValue;
    }

    function parseBool(key as String, defaultValue as Boolean) as Boolean {
        try {
            return parseBoolRaw(key, Application.Properties.getValue(key), defaultValue);
        } catch (e) {
            logE("Error parsing bool: " + key);
        }
        return defaultValue;
    }

    function parseBoolRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as Boolean
    ) as Boolean {
        try {
            if (value == null) {
                return false;
            }

            if (value instanceof String) {
                return (
                    value.equals("") ||
                    value.equals("false") ||
                    value.equals("False") ||
                    value.equals("FALSE") ||
                    value.equals("0")
                );
            }

            if (!(value instanceof Boolean)) {
                return false;
            }

            return value;
        } catch (e) {
            logE("Error parsing bool: " + key + " " + value);
        }
        return defaultValue;
    }

    function parseFloat(key as String, defaultValue as Float) as Float {
        try {
            return parseFloatRaw(key, Application.Properties.getValue(key), defaultValue);
        } catch (e) {
            logE("Error parsing float: " + key);
        }
        return defaultValue;
    }

    static function parseFloatRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as Float
    ) as Float {
        try {
            if (value == null) {
                return defaultValue;
            }

            if (
                value instanceof String ||
                value instanceof Float ||
                value instanceof Number ||
                value instanceof Double
            ) {
                // empty or invalid strings convert to null
                var ret = value.toFloat();
                if (ret == null) {
                    return defaultValue;
                }

                return ret;
            }

            return defaultValue;
        } catch (e) {
            logE("Error parsing float: " + key + " " + value);
        }
        return defaultValue;
    }

    function parseString(key as String, defaultValue as String) as String {
        try {
            return parseStringRaw(key, Application.Properties.getValue(key), defaultValue);
        } catch (e) {
            logE("Error parsing string: " + key);
        }
        return defaultValue;
    }

    function parseStringRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as String
    ) as String {
        try {
            if (value == null) {
                return defaultValue;
            }

            if (value instanceof String) {
                return value;
            }

            return defaultValue;
        } catch (e) {
            logE("Error parsing string: " + key + " " + value);
        }
        return defaultValue;
    }

    function parseOptionalFloat(key as String, defaultValue as Float?) as Float? {
        try {
            return parseOptionalFloatRaw(key, Application.Properties.getValue(key), defaultValue);
        } catch (e) {
            logE("Error parsing optional float: " + key);
        }
        return defaultValue;
    }

    function parseOptionalFloatRaw(
        key as String,
        value as PropertyValueType,
        defaultValue as Float?
    ) as Float? {
        try {
            if (value == null) {
                return null;
            }

            // as Float is a bit of a hack, it can be null, but we just want allow us to use our helper
            // (duck typing means at runtime the null passes through fine)
            return parseFloatRaw(key, value, defaultValue as Float);
        } catch (e) {
            logE("Error parsing optional float: " + key);
        }
        return defaultValue;
    }

    function getArraySchema(
        key as String,
        expectedKeys as Array<String>,
        parsers as Array<Method>,
        defaultValue as Array<Dictionary>
    ) as Array<Dictionary> {
        var value = null;
        try {
            value = Application.Storage.getValue(key);
            if (value == null) {
                return defaultValue;
            }

            if (!(value instanceof Array)) {
                return defaultValue;
            }

            // The dict we get is memory mapped, do not use it directly - need to create a copy so we can change the colour type from string to int
            // If we use it directly the storage value gets overwritten
            var result = [] as Array<Dictionary>;
            for (var i = 0; i < value.size(); ++i) {
                var entry = value[i];
                var entryOut = {};
                if (!(entry instanceof Dictionary)) {
                    return defaultValue;
                }

                for (var j = 0; j < expectedKeys.size(); ++j) {
                    var thisKey = expectedKeys[j];
                    var thisParser = parsers[j];
                    // back compat, if the keys are missing we need to default them
                    // old companion app will send route entries without the new keys
                    var keysValue = null;
                    if (entry.hasKey(thisKey)) {
                        keysValue = entry[thisKey];
                    }

                    entryOut[thisKey] = thisParser.invoke(key + "." + i + "." + thisKey, keysValue);
                }
                result.add(entryOut);
            }

            return result;
        } catch (e) {
            logE("Error parsing array: " + key + " " + value);
        }
        return defaultValue;
    }

    (:settingsView)
    function resetDefaultsFromMenu() as Void {
        // calling resetDefaults puts teh new values into our current state
        // we need to load
        // then we need to load them all back
        resetDefaultsInStorage();
        onSettingsChanged(); // reload anything that has changed
    }

    function resetDefaultsInStorage() as Void {
        logT("Resetting settings to default values");
        // resetDefaults flag is cleared by the asDict method
        var defaultSettings = new Settings();
        saveSettings(defaultSettings.asDict());
    }

    function asDict() as Dictionary<String, PropertyValueType> {
        // all these return values should be identical to the storage value
        // eg. nulls are exposed as 0
        // colours are strings

        return (
            ({
                "tileSize" => tileSize,
                "httpErrorTileTTLS" => httpErrorTileTTLS,
                "turnAlertTimeS" => turnAlertTimeS,
                "minTurnAlertDistanceM" => minTurnAlertDistanceM,
                "modeDisplayOrder" => encodeCSV(modeDisplayOrder),
                "dataFieldPageCounts" => encodeCSV(dataFieldPageCounts),
                "dataFieldPageTypes" => encodeCSV(dataFieldPageTypes),
                "maxTrackPoints" => maxTrackPoints,
                "trackStyle" => trackStyle,
                "trackWidth" => trackWidth,
                "showDirectionPointTextUnderIndex" => showDirectionPointTextUnderIndex,
                "errorTileTTLS" => errorTileTTLS,
                "fullTileSize" => fullTileSize,
                "activityType" => activityType(),
                "scaledTileSize" => scaledTileSize,
                "packingFormat" => packingFormat,
                "useDrawBitmap" => useDrawBitmap,
                "tileLayerMax" => tileLayerMax,
                "tileLayerMin" => tileLayerMin,
                "tileCacheSize" => tileCacheSize,
                "storageTileCacheSize" => storageTileCacheSize,
                "storageTileCachePageCount" => storageTileCachePageCount,
                "storageSeedBoundingBox" => storageSeedBoundingBox,
                "storageSeedRouteDistanceM" => storageSeedRouteDistanceM,
                "centerUserOffsetY" => centerUserOffsetY,
                "mapMoveScreenSize" => mapMoveScreenSize,
                "tileCachePadding" => tileCachePadding,
                "recalculateIntervalS" => recalculateIntervalS,
                "mode" => mode,
                "mapEnabled" => mapEnabled,
                "cacheTilesInStorage" => cacheTilesInStorage,
                "storageMapTilesOnly" => storageMapTilesOnly,
                "drawLineToClosestPoint" => drawLineToClosestPoint,
                "drawLineToClosestTrack" => drawLineToClosestTrack,
                "showTileBorders" => showTileBorders,
                "showErrorTileMessages" => showErrorTileMessages,
                "drawHitBoxes" => drawHitBoxes,
                "showDirectionPoints" => showDirectionPoints,
                "displayLatLong" => displayLatLong,
                "useStartForStop" => useStartForStop,
                "scaleRestrictedToTileLayers" => scaleRestrictedToTileLayers(),
                "trackColour" => trackColour.format("%X"),
                "trackColour2" => trackColour2.format("%X"),
                "defaultRouteColour" => defaultRouteColour.format("%X"),
                "tileErrorColour" => tileErrorColour.format("%X"),
                "elevationColour" => elevationColour.format("%X"),
                "userColour" => userColour.format("%X"),
                "maxPendingWebRequests" => maxPendingWebRequests,
                "metersAroundUser" => metersAroundUser,
                "zoomAtPaceMode" => zoomAtPaceMode,
                "zoomAtPaceSpeedMPS" => zoomAtPaceSpeedMPS,
                "useTrackAsHeadingSpeedMPS" => useTrackAsHeadingSpeedMPS,
                "topDataType" => topDataType,
                "bottomDataType" => bottomDataType,
                "autoLapDistanceM" => autoLapDistanceM,
                "minTrackPointDistanceM" => minTrackPointDistanceM,
                "trackPointReductionMethod" => trackPointReductionMethod,
                "dataFieldTextSize" => dataFieldTextSize,
                "uiMode" => uiMode,
                "elevationMode" => elevationMode,
                "alertType" => alertType,
                "renderMode" => renderMode,
                "fixedLatitude" => fixedLatitude == null ? 0f : fixedLatitude,
                "fixedLongitude" => fixedLongitude == null ? 0f : fixedLongitude,
                "tileUrl" => tileUrl,
                "authToken" => authToken,
                "mapChoice" => mapChoice,
                "routes" => routesToSave(),
                "routesEnabled" => routesEnabled,
                "displayRouteNames" => displayRouteNames,
                "disableMapsFailureCount" => disableMapsFailureCount,
                "enableOffTrackAlerts" => enableOffTrackAlerts,
                "offTrackWrongDirection" => offTrackWrongDirection,
                "drawCheverons" => drawCheverons,
                "offTrackAlertsDistanceM" => offTrackAlertsDistanceM,
                "offTrackAlertsMaxReportIntervalS" => offTrackAlertsMaxReportIntervalS,
                "offTrackCheckIntervalS" => offTrackCheckIntervalS,
                "normalModeColour" => normalModeColour.format("%X"),
                "routeMax" => _routeMax,
                "uiColour" => uiColour.format("%X"),
                "debugColour" => debugColour.format("%X"),
                "resetDefaults" => false,
            }) as Dictionary<String, PropertyValueType>
        );
    }

    function saveSettings(settings as Dictionary<String, PropertyValueType>) as Void {
        // should we sanitize this as its untrusted? makes it significantly more annoying to do
        var keys = settings.keys();
        for (var i = 0; i < keys.size(); ++i) {
            var key = keys[i] as String;
            var value = settings[key];
            // for now just blindly trust the users
            // we do reload which sanitizes, but they could break garmins settings page with unexpected types
            try {
                if (key.equals("routes")) {
                    Application.Storage.setValue(
                        key,
                        value as Dictionary<PropertyKeyType, PropertyValueType>
                    );
                } else {
                    Application.Properties.setValue(key, value as PropertyValueType);
                }
            } catch (e) {
                logE("failed property save: " + e.getErrorMessage() + " " + key + ":" + value);
                ++$.globalExceptionCounter;
            }
        }
    }

    function setup() as Void {
        // assert the map choice when we load the settings, as it may have been changed when the app was not running and onSettingsChanged might not be called
        loadSettings();
        updateMapChoiceChange(mapChoice);
        setMinTrackPointDistanceMSideEffect();
        recomputeTrackTexture();
        for (var i = 0; i < routes.size(); ++i) {
            recomputeRouteTexture(i);
        }
    }

    function parseSportAndSubSport(combinedValue as Number) as Void {
        // Decode the number back into sport and subSport
        // Example: 1003 / 1000 = 1 (SPORT_RUNNING)
        sport = combinedValue / 1000;
        // Example: 1003 % 1000 = 3 (SUB_SPORT_TRAIL)
        subSport = combinedValue % 1000;
    }

    function saveSportAndSubSport() as Void {
        setValue("activityType", activityType());
        sessionChanged();
    }

    function sessionChanged() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }
        _breadcrumbContextLocal.sessionChanged();
    }

    // todo split this off into setSport and setSubsport (then we can dynamically make the list for the menus and make them be able to pick a category then a sport in that category)
    // little gain doing that for now though
    // and it will only work in on-watch settings
    function setSportAndSubSport(combinedValue as Number) as Void {
        parseSportAndSubSport(combinedValue);
        saveSportAndSubSport();
    }

    function activityType() as Number {
        return sport * 1000 + subSport;
    }

    function loadSettingsPart1() as Void {
        var activityType = parseNumber("activityType", 0);
        parseSportAndSubSport(activityType);

        httpErrorTileTTLS = parseNumber("httpErrorTileTTLS", httpErrorTileTTLS);
        turnAlertTimeS = parseNumber("turnAlertTimeS", turnAlertTimeS);
        minTurnAlertDistanceM = parseNumber("minTurnAlertDistanceM", minTurnAlertDistanceM);
        maxTrackPoints = parseNumber("maxTrackPoints", maxTrackPoints);
        trackStyle = parseNumber("trackStyle", trackStyle);
        trackWidth = parseNumber("trackWidth", trackWidth);
        showDirectionPointTextUnderIndex = parseNumber(
            "showDirectionPointTextUnderIndex",
            showDirectionPointTextUnderIndex
        );
        errorTileTTLS = parseNumber("errorTileTTLS", errorTileTTLS);
        fullTileSize = parseNumber("fullTileSize", fullTileSize);
        useDrawBitmap = parseBool("useDrawBitmap", useDrawBitmap);
        packingFormat = parseNumber("packingFormat", packingFormat);
        scaledTileSize = parseNumber("scaledTileSize", scaledTileSize);
        if (useDrawBitmap) {
            scaledTileSize = fullTileSize;
        }
        tileUrl = parseString("tileUrl", tileUrl);
        tileSize = parseNumber("tileSize", tileSize);
        if (tileUrl.find(COMPANION_APP_TILE_URL_MATCH) == null) {
            tileSize = scaledTileSize;
        }
        tileLayerMax = parseNumber("tileLayerMax", tileLayerMax);
        tileLayerMin = parseNumber("tileLayerMin", tileLayerMin);
        // logT("tileSize: " + tileSize);
        if (tileSize < 2) {
            tileSize = 2;
        } else if (tileSize > 256) {
            tileSize = 256;
        }
        if (fullTileSize < 2) {
            fullTileSize = 2;
        } else if (fullTileSize > 256) {
            fullTileSize = 256;
        }
        if (scaledTileSize < 2) {
            scaledTileSize = 2;
        } else if (scaledTileSize > 256) {
            scaledTileSize = 256;
        }

        tileCacheSize = parseNumber("tileCacheSize", tileCacheSize);
        storageTileCacheSize = parseNumber("storageTileCacheSize", storageTileCacheSize);
        storageTileCachePageCount = parseNumber(
            "storageTileCachePageCount",
            storageTileCachePageCount
        );
        storageSeedBoundingBox = parseBool("storageSeedBoundingBox", storageSeedBoundingBox);
        storageSeedRouteDistanceM = parseFloat(
            "storageSeedRouteDistanceM",
            storageSeedRouteDistanceM
        );
        centerUserOffsetY = parseFloat("centerUserOffsetY", centerUserOffsetY);
        mapMoveScreenSize = parseFloat("mapMoveScreenSize", mapMoveScreenSize);
        tileCachePadding = parseNumber("tileCachePadding", tileCachePadding);
        recalculateIntervalS = parseNumber("recalculateIntervalS", recalculateIntervalS);
        recalculateIntervalS = recalculateIntervalS <= 0 ? 1 : recalculateIntervalS;
        mode = parseNumber("mode", mode);
        modeDisplayOrder = parseCSVString(
            "modeDisplayOrder",
            modeDisplayOrder,
            method(:defaultNumberParser)
        );
        dataFieldPageCounts = parseCSVString(
            "dataFieldPageCounts",
            dataFieldPageCounts,
            method(:defaultNumberParser)
        );
        dataFieldPageTypes = parseCSVString(
            "dataFieldPageTypes",
            dataFieldPageTypes,
            method(:defaultNumberParser)
        );
        saveDataFieldPages();
        mapEnabled = parseBool("mapEnabled", mapEnabled);
        setMapEnabledRaw(mapEnabled); // prompt for app to open if needed
        cacheTilesInStorage = parseBool("cacheTilesInStorage", cacheTilesInStorage);
        storageMapTilesOnly = parseBool("storageMapTilesOnly", storageMapTilesOnly);
        drawLineToClosestPoint = parseBool("drawLineToClosestPoint", drawLineToClosestPoint);
        drawLineToClosestTrack = parseBool("drawLineToClosestTrack", drawLineToClosestTrack);
        showTileBorders = parseBool("showTileBorders", showTileBorders);
        showErrorTileMessages = parseBool("showErrorTileMessages", showErrorTileMessages);
        drawHitBoxes = parseBool("drawHitBoxes", drawHitBoxes);
        showDirectionPoints = parseBool("showDirectionPoints", showDirectionPoints);
        displayLatLong = parseBool("displayLatLong", displayLatLong);
        useStartForStop = parseBool("useStartForStop", useStartForStop);
        _scaleRestrictedToTileLayers = parseBool(
            "scaleRestrictedToTileLayers",
            _scaleRestrictedToTileLayers
        );
        displayRouteNames = parseBool("displayRouteNames", displayRouteNames);
        enableOffTrackAlerts = parseBool("enableOffTrackAlerts", enableOffTrackAlerts);
        offTrackWrongDirection = parseBool("offTrackWrongDirection", offTrackWrongDirection);
        drawCheverons = parseBool("drawCheverons", drawCheverons);
        routesEnabled = parseBool("routesEnabled", routesEnabled);
        trackColour = parseColour("trackColour", trackColour);
        trackColour2 = parseColourTransparency("trackColour2", trackColour2, true);
        defaultRouteColour = parseColour("defaultRouteColour", defaultRouteColour);
        tileErrorColour = parseColour("tileErrorColour", tileErrorColour);
        elevationColour = parseColour("elevationColour", elevationColour);
        userColour = parseColour("userColour", userColour);
        normalModeColour = parseColour("normalModeColour", normalModeColour);
    }

    function loadSettingsPart2() as Void {
        _routeMax = parseColour("routeMax", _routeMax);
        uiColour = parseColour("uiColour", uiColour);
        debugColour = parseColour("debugColour", debugColour);
        maxPendingWebRequests = parseNumber("maxPendingWebRequests", maxPendingWebRequests);
        metersAroundUser = parseNumber("metersAroundUser", metersAroundUser);
        zoomAtPaceMode = parseNumber("zoomAtPaceMode", zoomAtPaceMode);
        zoomAtPaceSpeedMPS = parseFloat("zoomAtPaceSpeedMPS", zoomAtPaceSpeedMPS);
        useTrackAsHeadingSpeedMPS = parseFloat(
            "useTrackAsHeadingSpeedMPS",
            useTrackAsHeadingSpeedMPS
        );
        topDataType = parseNumber("topDataType", topDataType);
        bottomDataType = parseNumber("bottomDataType", bottomDataType);
        autoLapDistanceM = parseNumber("autoLapDistanceM", autoLapDistanceM);
        minTrackPointDistanceM = parseNumber("minTrackPointDistanceM", minTrackPointDistanceM);
        trackPointReductionMethod = parseNumber(
            "trackPointReductionMethod",
            trackPointReductionMethod
        );
        dataFieldTextSize = parseNumber("dataFieldTextSize", dataFieldTextSize);
        uiMode = parseNumber("uiMode", uiMode);
        elevationMode = parseNumber("elevationMode", elevationMode);
        alertType = parseNumber("alertType", alertType);
        renderMode = parseNumber("renderMode", renderMode);

        fixedLatitude = parseOptionalFloat("fixedLatitude", fixedLatitude);
        fixedLongitude = parseOptionalFloat("fixedLongitude", fixedLongitude);
        setFixedPositionWithoutUpdate(fixedLatitude, fixedLongitude, false);
        updateRequiresAuth();
        authToken = parseString("authToken", authToken);
        mapChoice = parseNumber("mapChoice", mapChoice);
        routes = getArraySchema(
            "routes",
            ["routeId", "name", "enabled", "colour", "colour2", "reversed", "style", "width"],
            [
                method(:defaultNumberParser),
                method(:emptyString),
                method(:defaultFalse),
                method(:defaultColourParser),
                method(:defaultColourParserTransparent),
                method(:defaultFalse),
                method(:defaultNumberParser),
                method(:defaultNumberParser4),
            ],
            routes
        );
        logT("parsed routes: " + routes);
        disableMapsFailureCount = parseNumber("disableMapsFailureCount", disableMapsFailureCount);
        offTrackAlertsDistanceM = parseNumber("offTrackAlertsDistanceM", offTrackAlertsDistanceM);
        offTrackAlertsMaxReportIntervalS = parseNumber(
            "offTrackAlertsMaxReportIntervalS",
            offTrackAlertsMaxReportIntervalS
        );
        offTrackCheckIntervalS = parseNumber("offTrackCheckIntervalS", offTrackCheckIntervalS);
    }

    // Load the values initially from storage
    function loadSettings() as Void {
        // fix for a garmin bug where bool settings are not changable if they default to true
        // https://forums.garmin.com/developer/connect-iq/i/bug-reports/bug-boolean-properties-with-default-value-true-can-t-be-changed-in-simulator
        var haveDoneFirstLoadSetup = Application.Properties.getValue("haveDoneFirstLoadSetup");
        if (haveDoneFirstLoadSetup instanceof Boolean && !haveDoneFirstLoadSetup) {
            setValue("haveDoneFirstLoadSetup", true);
            resetDefaultsInStorage(); // puts the default values into storage
        }

        var resetDefaults = Application.Properties.getValue("resetDefaults") as Boolean;
        if (resetDefaults) {
            resetDefaultsInStorage(); // puts the default values into storage
            return;
        }

        var returnToUser = Application.Properties.getValue("returnToUser") as Boolean;
        if (returnToUser) {
            Application.Properties.setValue("returnToUser", false);
            var _breadcrumbContextLocal = $._breadcrumbContext;
            if (_breadcrumbContextLocal != null) {
                _breadcrumbContextLocal.cachedValues.returnToUser();
            }
        }

        logT("loadSettings: Loading all settings");
        loadSettingsPart1();
        loadSettingsPart2();

        // testing coordinates (piper-comanche-wreck)
        // setFixedPosition(-27.297773, 152.753883);
        // // cachedValues.setScale(0.39); // zoomed out a bit
        // cachedValues.setScale(1.96); // really close
    }

    function emptyString(key as String, value as PropertyValueType) as String {
        return parseStringRaw(key, value, "");
    }

    function defaultNumberParser(key as String, value as PropertyValueType) as Number {
        return parseNumberRaw(key, value, 0);
    }

    (:blackAndWhite)
    function defaultNumberParser4(key as String, value as PropertyValueType) as Number {
        return parseNumberRaw(key, value, 4);
    }

    (:fullColours)
    function defaultNumberParser4(key as String, value as PropertyValueType) as Number {
        return parseNumberRaw(key, value, 4);
    }

    function defaultFalse(key as String, value as PropertyValueType) as Boolean {
        if (value instanceof Boolean) {
            return value;
        }

        return false;
    }

    function defaultColourParser(key as String, value as PropertyValueType) as Number {
        return parseColourRaw(key, value, Graphics.COLOR_RED, false);
    }

    function defaultColourParserTransparent(key as String, value as PropertyValueType) as Number {
        return parseColourRaw(key, value, Graphics.COLOR_TRANSPARENT, true);
    }

    function onSettingsChanged() as Void {
        var _breadcrumbContextLocal = $._breadcrumbContext;
        if (_breadcrumbContextLocal == null) {
            breadcrumbContextWasNull();
            return;
        }

        logT("onSettingsChanged: Setting Changed, loading");
        var oldSport = sport;
        var oldSubSport = subSport;
        var oldRoutes = routes;
        var oldRouteMax = _routeMax;
        var oldMapChoice = mapChoice;
        var oldTileUrl = tileUrl;
        var oldTileSize = tileSize;
        var oldHttpErrorTileTTLS = httpErrorTileTTLS;
        var oldMaxTrackPoints = maxTrackPoints;
        var oldErrorTileTTLS = errorTileTTLS;
        var oldFullTileSize = fullTileSize;
        var oldScaledTileSize = scaledTileSize;
        var oldPackingFormat = packingFormat;
        var oldUseDrawBitmap = useDrawBitmap;
        var oldTileCacheSize = tileCacheSize;
        var oldStorageTileCacheSize = storageTileCacheSize;
        var oldStorageTileCachePageCount = storageTileCachePageCount;
        var oldMapEnabled = mapEnabled;
        var oldCacheTilesInStorage = cacheTilesInStorage;
        var oldAuthToken = authToken;
        var oldTileErrorColour = tileErrorColour;
        var oldShowErrorTileMessages = showErrorTileMessages;
        var oldMinTrackPointDistanceM = minTrackPointDistanceM;
        var oldTrackStyle = trackStyle;
        var oldTrackWidth = trackWidth;
        var oldTrackColour = trackColour;
        var oldTrackColour2 = trackColour2;
        var oldUseStartForStop = useStartForStop;
        loadSettings();
        // route settins do not work because garmins setting spage cannot edit them
        // when any property is modified, so we have to explain to users not to touch the settings, but we cannot because it looks
        // like garmmins settings are not rendering desciptions anymore :(
        for (var i = 0; i < oldRoutes.size(); ++i) {
            var oldRouteEntry = oldRoutes[i];
            var oldRouteId = oldRouteEntry["routeId"] as Number;

            var routeIndex = getRouteIndexById(oldRouteId);
            if (routeIndex != null) {
                // we have the same route
                var currentRouteEntry = routes[routeIndex];
                if (oldRouteEntry["reversed"] != currentRouteEntry["reversed"]) {
                    _breadcrumbContextLocal.reverseRouteId(oldRouteId);
                }

                var currentStyle = currentRouteEntry["style"] as Number;
                var currentWidth = currentRouteEntry["width"] as Number;
                var currentColour = currentRouteEntry["colour"] as Number;
                var currentColour2 = currentRouteEntry["colour2"] as Number;
                if (
                    oldRouteEntry["style"] != currentStyle ||
                    oldRouteEntry["width"] != currentWidth ||
                    oldRouteEntry["colour"] != currentColour ||
                    oldRouteEntry["colour2"] != currentColour2
                ) {
                    recomputeRouteTexture(routeIndex);
                }

                continue;
            }

            // clear the route
            clearRouteFromContext(oldRouteId);
        }

        if (oldSport != sport || oldSubSport != subSport) {
            sessionChanged();
        }

        if (oldRouteMax > _routeMax) {
            routeMaxReduced();
        }

        if (
            oldTileErrorColour != tileErrorColour ||
            oldShowErrorTileMessages != showErrorTileMessages
        ) {
            _breadcrumbContextLocal.tileCache.clearValuesWithoutStorage(); // remove the errored tiles from cache and redraw
        }

        // run any tile cache clearing that we need to when map features change
        if (!oldTileUrl.equals(tileUrl)) {
            tileUrlChanged();
        }
        if (
            oldTileSize != tileSize ||
            oldHttpErrorTileTTLS != httpErrorTileTTLS ||
            oldErrorTileTTLS != errorTileTTLS ||
            oldFullTileSize != fullTileSize ||
            oldScaledTileSize != scaledTileSize ||
            oldUseDrawBitmap != useDrawBitmap ||
            oldPackingFormat != packingFormat ||
            oldCacheTilesInStorage != cacheTilesInStorage ||
            !oldAuthToken.equals(authToken)
        ) {
            tileServerPropChanged();
        }

        if (oldStorageTileCacheSize > storageTileCacheSize) {
            storageTileCacheSizeReduced();
        }

        if (oldStorageTileCachePageCount != storageTileCachePageCount) {
            storageTilePageCountChanged();
        }

        if (oldTileCacheSize > tileCacheSize) {
            tileCacheSizeReduced();
        }

        if (oldMaxTrackPoints != maxTrackPoints) {
            maxTrackPointsChanged();
        }

        if (oldMapEnabled != mapEnabled) {
            mapEnabledChanged();
        }

        if (oldMapChoice != mapChoice) {
            updateMapChoiceChange(mapChoice);
        }

        if (oldMinTrackPointDistanceM != minTrackPointDistanceM) {
            setMinTrackPointDistanceMSideEffect();
        }

        if (
            oldTrackStyle != trackStyle ||
            oldTrackWidth != trackWidth ||
            oldTrackColour != trackColour ||
            oldTrackColour2 != trackColour2
        ) {
            recomputeTrackTexture();
        }

        if (oldUseStartForStop != useStartForStop) {
            useStartForStopSideEffect();
        }

        setValueSideEffect();
    }
}

// As the number of settings and number of cached variables updated are increasing stack overflows are becoming more common
// I think the main issue is the setBlah methods are meant to be used for on app settings, so they all call into setValue()
// but we need to not do that when we are comming from the context of onSettingsChanged, since we manually call the updateCachedValues at the end of onSettingsChanged

// Error: Stack Overflow Error
// Details: 'Failed invoking <symbol>'
// Time: 2025-05-14T11:00:57Z
// Part-Number: 006-B3704-00
// Firmware-Version: '19.05'
// Language-Code: eng
// ConnectIQ-Version: 5.1.1
// Filename: BreadcrumbDataField
// Appname: BreadcrumbDataField
// Stack:
//   - pc: 0x10002541
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 875
//     Function: getRouteIndexById
//   - pc: 0x100024ef
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 813
//     Function: routeEnabled
//   - pc: 0x10008ec0
//     File: 'BreadcrumbDataField\source\CachedValues.mc'
//     Line: 114
//     Function: calcOuterBoundingBoxFromTrackAndRoutes
//   - pc: 0x1000833a
//     File: 'BreadcrumbDataField\source\CachedValues.mc'
//     Line: 170
//     Function: getNewScaleAndUpdateCenter
//   - pc: 0x100092f2
//     File: 'BreadcrumbDataField\source\CachedValues.mc'
//     Line: 128
//     Function: updateScaleCenterAndMap
//   - pc: 0x100093c8
//     File: 'BreadcrumbDataField\source\CachedValues.mc'
//     Line: 440
//     Function: recalculateAll
//   - pc: 0x100043d6
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 1169
//     Function: updateCachedValues
//   - pc: 0x10004359
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 417
//     Function: setValue
//   - pc: 0x10002a86
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 649
//     Function: setTileLayerMax
//   - pc: 0x10003948
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 541
//     Function: updateMapChoiceChange
//   - pc: 0x10003ff6
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 428
//     Function: setMapChoice
//   - pc: 0x10001e3e
//     File: 'BreadcrumbDataField\source\Settings.mc'
//     Line: 1817
//     Function: onSettingsChanged
//   - pc: 0x10006d39
//     File: 'BreadcrumbDataField\source\BreadcrumbDataFieldApp.mc'
//     Line: 253
//     Function: onPhone
